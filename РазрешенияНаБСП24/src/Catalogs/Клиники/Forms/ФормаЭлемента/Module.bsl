#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
#Область СтандартныеПодсистемы

	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
#КонецОбласти

	//Если Объект.ЭтоОтправитель И Не Пользователи.ЭтоПолноправныйПользователь() Тогда
	//	ЭтаФорма.Доступность = Ложь;
	//КонецЕсли;
	
	//Элементы.ЭтоОтправитель.Видимость = Пользователи.ЭтоПолноправныйПользователь();
	
	ЗаполнитьРеквизитыКонтактногоЛицаНаФорме();

	ТекущаяСтрана = Объект.Страна;
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

// Процедура обработчик события ПриОткрытии
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры // ПриОткрытии()

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
		
	//Если Объект.ЭтоОтправитель И Не ЗначениеЗаполнено(Объект.КлиентскийНомер) Тогда
	//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнен клиентский номер отправителя!", , "КлиентскийНомер", "Объект", Отказ);	
	//КонецЕсли;
	
	Если Не ПустаяСтрока(КонтактноеЛицоНаименование) Или Не ПустаяСтрока(КонтактноеЛицоТелефонныйКод) Или Не ПустаяСтрока(КонтактноеЛицоТелефон) Или Не ПустаяСтрока(КонтактноеЛицоЭлектроннаяПочта) Тогда
		
		Если ПустаяСтрока(КонтактноеЛицоНаименование) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнено наименование контактного лица!", 	, "КонтактноеЛицоНаименование", 	, Отказ);	
		КонецЕсли;
		
		Если ПустаяСтрока(КонтактноеЛицоТелефонныйКод) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнен телефонный код контактного лица!", 	, "КонтактноеЛицоТелефонныйКод", 	, Отказ);	
		КонецЕсли;
		
		Если ПустаяСтрока(КонтактноеЛицоТелефон) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнен телефон контактного лица!", 			, "КонтактноеЛицоТелефон", 			, Отказ);	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ЗаписатьКонтактноеЛицо();
	ЭтаФорма.Прочитать();
	
КонецПроцедуры

// Процедура обработчик события ОбработкаОповещения.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Объект.НаименованиеТранслит = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Объект.Наименование);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоПриИзменении(Элемент)
	
	ЗаполнитьРеквизитыКонтактногоЛицаНаФорме();
	ДанныеКонтактногоЛицаИзменены = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ГородПриИзменении(Элемент)
	
	Объект.ГородТранслит = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Объект.Город);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПриИзменении(Элемент)
	
	Объект.АдресТранслит = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Объект.Адрес);
	
КонецПроцедуры

&НаКлиенте
Процедура ИндексПриИзменении(Элемент)
	
	Если Объект.Страна <> ПредопределенноеЗначение("Справочник.СтраныМира.Россия") Тогда
		Возврат;
	КонецЕсли;
	
	ПолучитьГородПоИндексу();
	
	Если Элементы.ГородТранслит.СписокВыбора.Количество() > 0 Тогда
		Элементы.ГородТранслит.СписокВыбора.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ВыборГородаЗавершение", ЭтаФорма), "Выберите город");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресТранслитПриИзменении(Элемент)

	Объект.АдресТранслит = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Объект.АдресТранслит);	
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеАнглийскоеПриИзменении(Элемент)
	
	Объект.НаименованиеТранслит = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Объект.НаименованиеТранслит);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоНаименованиеПриИзменении(Элемент)
	
	ДанныеКонтактногоЛицаПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоЭлектроннаяПочтаПриИзменении(Элемент)
	
	ДанныеКонтактногоЛицаПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоТелефонныйКодПриИзменении(Элемент)
	
	ДанныеКонтактногоЛицаПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоТелефонПриИзменении(Элемент)
	
	ДанныеКонтактногоЛицаПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура СтранаПриИзменении(Элемент)
	
	Элементы.ГородТранслит.РедактированиеТекста 	= Объект.Страна <> ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	Элементы.ГородТранслит.КнопкаВыпадающегоСписка 	= Объект.Страна = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	
	Если ТекущаяСтрана <> Объект.Страна Тогда
		Объект.Индекс 			= "";
		Объект.ГородТранслит 	= "";
	КонецЕсли;
	
	ТекущаяСтрана = Объект.Страна;
	
КонецПроцедуры

&НаКлиенте
Процедура ГородТранслитПриИзменении(Элемент)
	
	Объект.ГородТранслит = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Объект.ГородТранслит);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРеквизитыКонтактногоЛицаНаФорме()
	
	КонтактноеЛицоНаименование 		= Объект.КонтактноеЛицо.Наименование;
	КонтактноеЛицоЭлектроннаяПочта 	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Объект.КонтактноеЛицо, Справочники.ВидыКонтактнойИнформации.EmailКонтактногоЛица);
	КонтактноеЛицоТелефонныйКод 	= Объект.КонтактноеЛицо.ТелефонныйКод;
	КонтактноеЛицоТелефон 			= Объект.КонтактноеЛицо.Телефон;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьКонтактноеЛицо()
	
	Если ДанныеКонтактногоЛицаИзменены Тогда
		
		Если Объект.КонтактноеЛицо.Пустая() Тогда
			КонтактноеЛицо = Справочники.КонтактныеЛица.СоздатьЭлемент();
		Иначе
			КонтактноеЛицо = Объект.КонтактноеЛицо.ПолучитьОбъект();
		КонецЕсли;
		
		КонтактноеЛицо.Наименование 		= КонтактноеЛицоНаименование;
		КонтактноеЛицо.НаименованиеТранслит = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(КонтактноеЛицоНаименование);
		
		УправлениеКонтактнойИнформациейРазрешения1C.ДобавитьАдресЭлектроннойПочтыКонтактногоЛица(КонтактноеЛицо, КонтактноеЛицоЭлектроннаяПочта, Истина);
		КонтактноеЛицо.ТелефонныйКод 		= КонтактноеЛицоТелефонныйКод;
		КонтактноеЛицо.Телефон 				= КонтактноеЛицоТелефон;
		КонтактноеЛицо.Владелец 			= Объект.Ссылка;
		
		КонтактноеЛицо.Записать();
		
		ТекущийОбъект = РеквизитФормыВЗначение("Объект");		
		ТекущийОбъект.КонтактноеЛицо = КонтактноеЛицо.Ссылка;
		ТекущийОбъект.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеКонтактногоЛицаПриИзменении()
	
	Модифицированность 				= Истина;
	ДанныеКонтактногоЛицаИзменены 	= Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьГородПоИндексу()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Индекс", Объект.Индекс);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИндексыГородов.Город КАК Город
	|ИЗ
	|	РегистрСведений.ИндексыГородов КАК ИндексыГородов
	|ГДЕ
	|	ИндексыГородов.Индекс = &Индекс
	|
	|СГРУППИРОВАТЬ ПО
	|	ИндексыГородов.Город
	|
	|УПОРЯДОЧИТЬ ПО
	|	Город";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		Объект.ГородТранслит = "";
		Сообщить("Город не поддерживается! Проверьте корректность индекса.");
		Возврат;
	КонецЕсли;
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Объект.ГородТранслит = Выборка.Город;
		Возврат;
	КонецЕсли;
	
	Элементы.ГородТранслит.СписокВыбора.Очистить();
	Объект.ГородТранслит = "";
	Пока Выборка.Следующий() Цикл
		Элементы.ГородТранслит.СписокВыбора.Добавить(Выборка.Город);	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборГородаЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		Объект.ГородТранслит = ВыбранныйЭлемент.Значение;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма, РеквизитФормыВЗначение("Объект"));
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти