
#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ВыгрузитьСписокРазрешений(Команда)
	
	ФайлыВоВременномХранилище = ПоместитьТабличныеДокументыВоВременноеХранилище();
	СохранитьПечатныеФормыВПапку(ФайлыВоВременномХранилище, ВыбраннаяПапка);
	
КонецПроцедуры 
	
#КонецОбласти  

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПутьСохраненияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ДиалогВыбораПапки = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Если Не ПустаяСтрока(ВыбраннаяПапка) Тогда
		ДиалогВыбораПапки.Каталог = ВыбраннаяПапка;
	КонецЕсли;
	
	Если ДиалогВыбораПапки.Выбрать() Тогда
		ВыбраннаяПапка = ДиалогВыбораПапки.Каталог;
		ОчиститьСообщения();
	КонецЕсли;

КонецПроцедуры
	
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПоместитьТабличныеДокументыВоВременноеХранилище()
	
	Период = Объект.Период;
	ТабличныйДокумент = СформироватьТабличныйДокумент();
	
	Результат = Новый Массив;
	ИмяФайла = "Example с " + Формат(Период.ДатаНачала, "ДФ=dd-MM-yyyy") + " по " + Формат(Период.ДатаОкончания, "ДФ=dd-MM-yyyy") + ".xlsx";

	// подготовка временной папки
	ИмяВременнойПапки = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ИмяВременнойПапки);
	
	ПолноеИмяФайла = УникальноеИмяФайла(ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяВременнойПапки) + ИмяФайла);
	ТабличныйДокумент.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяФайла);
	ПутьВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ЭтотОбъект.УникальныйИдентификатор);
	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("Представление", ИмяФайла);
	ОписаниеФайла.Вставить("АдресВоВременномХранилище", ПутьВоВременномХранилище);
	Результат.Добавить(ОписаниеФайла);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция СформироватьТабличныйДокумент()
	
	Период = Объект.Период;
	ТабличныйДокумент = Новый ТабличныйДокумент;
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("СписокЛабораторий", Объект.Лаборатории.Выгрузить(,"ReceiverLAB"));
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НакладныеРазрешения.Ссылка КАК НакладнаяСсылка,
		|	Разрешения.Ссылка КАК РазрешениеСсылка,
		|	НакладныеРазрешения.Ссылка.Номер КАК НомерНакладной,
		|	Разрешения.StudyNumber,
		|	Разрешения.RegisatrationLetterNr,
		|	Разрешения.Issued,
		|	Разрешения.TrialPermit,
		|	Разрешения.Issued2,
		|	ПринимающаяЛаборатория.Наименование КАК ReceiverLAB
		|ИЗ
		|	Документ.Накладные.Разрешения КАК НакладныеРазрешения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Разрешения КАК Разрешения
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПринимающаяЛаборатория КАК ПринимающаяЛаборатория
		|			ПО Разрешения.ReceiverLAB = ПринимающаяЛаборатория.Ссылка
		|		ПО НакладныеРазрешения.Разрешение = Разрешения.Ссылка
		|ГДЕ
		|	НакладныеРазрешения.Ссылка.ДатаВывоза МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И Разрешения.ReceiverLAB В(&СписокЛабораторий)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Макет = Обработки.ВыгрузкаСпискаНакладных.ПолучитьМакет("ПФ_MXL_Example");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Номер = 0;
	Пока Выборка.Следующий() Цикл
		Номер = Номер + 1;
		ОбластьСтрока.Параметры.Заполнить(Выборка);
		ОбластьСтрока.Параметры.Номер = Номер;
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

&НаКлиенте
Процедура СохранитьПечатныеФормыВПапку(СписокФайловВоВременномХранилище, Знач Папка = "")
	
	#Если ВебКлиент Тогда
		Для Каждого ФайлДляЗаписи Из СписокФайловВоВременномХранилище Цикл
			ПолучитьФайл(ФайлДляЗаписи.АдресВоВременномХранилище, ФайлДляЗаписи.Представление);
		КонецЦикла;
		Возврат;
	#КонецЕсли
	
	Папка = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Папка);
	Для Каждого ФайлДляЗаписи Из СписокФайловВоВременномХранилище Цикл
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ФайлДляЗаписи.АдресВоВременномХранилище);
		ДвоичныеДанные.Записать(УникальноеИмяФайла(Папка + ФайлДляЗаписи.Представление));
	КонецЦикла;
	
	Состояние(НСтр("ru = 'Сохранение успешно завершено'"), , НСтр("ru = 'в папку:'") + " " + Папка);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УникальноеИмяФайла(ИмяФайла)
	
	Файл = Новый Файл(ИмяФайла);
	ИмяБезРасширения = Файл.ИмяБезРасширения;
	Расширение = Файл.Расширение;
	Папка = Файл.Путь;
	
	Счетчик = 1;
	Пока Файл.Существует() Цикл
		Счетчик = Счетчик + 1;
		Файл = Новый Файл(Папка + ИмяБезРасширения + " (" + Счетчик + ")" + Расширение);
	КонецЦикла;
	
	Возврат Файл.ПолноеИмя;

КонецФункции

#КонецОбласти 
