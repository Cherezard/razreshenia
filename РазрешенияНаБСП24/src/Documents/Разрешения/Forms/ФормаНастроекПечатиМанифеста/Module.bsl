
#Область ОбработчикиСобытийФорм

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПараметрыПечати") Тогда
		ПараметрыПечати = Параметры.ПараметрыПечати;
	Иначе
		Возврат;
	КонецЕсли; 
	
	ОписаниеКоманды = Параметры.ОписаниеКоманды;
	ВладелецФормыПечати = Параметры.ВладелецФормы;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресФаксимильнойПодписьНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ЗаблокироватьДанныеФормыДляРедактирования();
	
	ДобавитьИзображениеФаксимилеФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПодписиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗаблокироватьДанныеФормыДляРедактирования();
	
	ДобавитьИзображениеПодписиФрагмент();
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеРеквизитов(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Закрыть();
	
	ДопПараметры = ПараметрыПечати.ДополнительныеПараметры;
	ДопПараметры.Вставить("Период", ДатаДокумента);
	ДопПараметры.Вставить("ТаможенныйПост", ТаможенныйПост);
	
	Если ЗначениеЗаполнено(АдресФаксимильнойПечати) Тогда
		ДопПараметры.Вставить("АдресФаксимильнойПечати", АдресФаксимильнойПечати);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АдресФаксимильнойПодписи) Тогда
		ДопПараметры.Вставить("АдресФаксимильнойПодписи", АдресФаксимильнойПодписи);
	КонецЕсли;
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ОписаниеКоманды.МенеджерПечати, ОписаниеКоманды.Идентификатор, ОписаниеКоманды.ОбъектыПечати,
		ВладелецФормыПечати, ПараметрыПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеПодписи(Команда)
	
	ДобавитьИзображениеПодписиФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеПодписиФрагмент()
    
	Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораПодписи", ЭтотОбъект);
	НачатьПомещениеФайла(Оповещение,,, Истина, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИзображениеПодписи(Команда)
	
	ОчиститьСообщения();
	
	АдресФаксимильнойПодписи = "";
	ДобавитьИзображениеПодписиФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИзображениеПодписи(Команда)
	
	АдресФаксимильнойПодписи = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеФаксимиле(Команда)
	
	ДобавитьИзображениеФаксимилеФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеФаксимилеФрагмент()
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораФаксимили", ЭтотОбъект);
	НачатьПомещениеФайла(Оповещение,,, Истина, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИзображениеФаксимиле(Команда)
	
	ОчиститьСообщения();

	АдресФаксимильнойПечати = "";
	ДобавитьИзображениеФаксимилеФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИзображениеФаксимиле(Команда)
	
	АдресФаксимильнойПечати = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитов(Отказ)

	СписокРеквизитов = Новый СписокЗначений;
	СписокРеквизитов.Добавить(ДатаДокумента, Элементы.ДатаДокумента.Имя);
	СписокРеквизитов.Добавить(ТаможенныйПост, Элементы.ТаможенныйПост.Имя);
	
	Для каждого Реквизит Из СписокРеквизитов Цикл
		Если Не ЗначениеЗаполнено(Реквизит.Значение) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнен реквизит " + Реквизит.Представление,, Реквизит.Представление,, Отказ);
		КонецЕсли; 
	КонецЦикла; 

КонецПроцедуры 

&НаКлиенте
Процедура ВыбратьКартинкуИзПрисоединенныхФайлов()
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ВладелецФайла", Объект.Ссылка);
	ПараметрыОтбора.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОтбора.Вставить("РежимВыбора", Истина);
	
	ОткрытьФорму("ОбщаяФорма.ПрисоединенныеФайлы", ПараметрыОтбора, ЭтаФорма);
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаВыбораПодписи(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли; 
	
	АдресФаксимильнойПодписи = Адрес;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораФаксимили(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли; 
	
	АдресФаксимильнойПечати = Адрес;
	
КонецПроцедуры

#КонецОбласти
