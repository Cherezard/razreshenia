#Область ПроцедурыИФункцииБазовые 

// Функция, вызываемая перед началом работы системы.
//
Функция ПередНачаломРаботыСистемы() Экспорт
	
	Если глПодключаемоеОборудование = Неопределено Тогда
		глПодключаемоеОборудование = Новый Структура();
		глПодключаемоеОборудование.Вставить("ДрайвераПодключаемогоОборудования", Новый Соответствие());
		глПодключаемоеОборудование.Вставить("ПараметрыПодключенияПО"           , Новый Массив());
		глПодключаемоеОборудование.Вставить("ПоследнийСлипЧек"                 , "");
		глПодключаемоеОборудование.Вставить("ЗавершениеРаботыСистемы"          , Ложь);
	КонецЕсли;
	
КонецФункции

// Функция, вызываемая при начале работы системы.
//
Функция ПриНачалеРаботыСистемы() Экспорт
	
#Если Не ВебКлиент Тогда
	ПереустановитьПомеченныеДрайверы();
#КонецЕсли
	
КонецФункции

// Функция, вызываемая при начале работы системы.
// Выполняет подготовку данных механизма.
Функция ПередЗавершениемРаботыСистемы() Экспорт
	
	НачатьОтключениеВсегоОборудования();
	
КонецФункции

// Поиск по идентификатору подключенного ранее устройства. 
//
Функция ПолучитьПодключенноеУстройство(СписокПодключений, Идентификатор) Экспорт
	
	ПодключенноеУстройство = Неопределено;
	
	Для Каждого Подключение Из СписокПодключений Цикл
		Если Подключение.Ссылка = Идентификатор Тогда
			ПодключенноеУстройство = Подключение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПодключенноеУстройство;
	
КонецФункции

// Поиск по типу подключенные ранее устройства.
//
Функция ПолучитьПодключенныеУстройства(СписокПодключений, ТипПО, Идентификатор = Неопределено) Экспорт
	
	ПодключенныеУстройства = Новый Массив();
	
	Для Каждого Подключение Из СписокПодключений Цикл
		Если Подключение.ТипОборудования = ТипПО Тогда
			Если (Идентификатор <> Неопределено) Тогда
				Если (Подключение.Ссылка = Идентификатор) Тогда
					ПодключенныеУстройства.Добавить(Подключение);
					Прервать;
				КонецЕсли;
			Иначе
				ПодключенныеУстройства.Добавить(Подключение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПодключенныеУстройства;
	
КонецФункции

// Выполнить настройку оборудования.
// 
Процедура ВыполнитьНастройкуОборудования(Идентификатор, ОповещениеПриЗавершении = Неопределено) Экспорт

	Результат = Истина;
	
	ДанныеУстройства = МенеджерОборудованияКлиентПовтИсп.ПолучитьДанныеУстройства(Идентификатор);
	ПараметрыФормы = Новый Структура("ПараметрыОборудования", ДанныеУстройства.Параметры);
	ПараметрыФормы.Вставить("Идентификатор", Идентификатор);       
	ПараметрыФормы.Вставить("ДрайверОборудования", ДанныеУстройства.ДрайверОборудования);  
	ПараметрыФормы.Вставить("ВерсияФорматаОбмена", ДанныеУстройства.ВерсияФорматаОбмена);
	
	ФормаНастройки = "ФормаНастройкиУниверсальныйДрайвер";
	
	ОбработчикДрайвера = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(ДанныеУстройства.ОбработчикДрайвера, Не ДанныеУстройства.ВСоставеКонфигурации, ДанныеУстройства.ТипОборудованияИмя);
		
	Если Не ОбработчикДрайвера = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент 
		И Не ОбработчикДрайвера = ПодключаемоеОборудованиеУниверсальныйДрайверАсинхронноКлиент  
		Тогда
			ФормаНастройки = СтрЗаменить(ДанныеУстройства.ОбработчикДрайвераИмя, "Обработчик", "ФормаНастройки");
	КонецЕсли;
		
	Если НЕ ПустаяСтрока(ФормаНастройки) Тогда
		ПараметрыКоманды = Новый Структура("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
		Обработчик = Новый ОписаниеОповещения("ВыполнитьНастройкуОборудования_Завершение", ЭтотОбъект, ПараметрыКоманды);
		ОткрытьФорму("ОбщаяФорма." + ФормаНастройки, ПараметрыФормы,,,  ,, Обработчик, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Произошла ошибка инициализации формы настройки драйвера.'")); 
	КонецЕсли;
	
КонецПроцедуры

// Завершение настройки оборудования.
//
Процедура ВыполнитьНастройкуОборудования_Завершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		РезультатЗавершения = Ложь;
		Если Результат.Свойство("Идентификатор") И Результат.Свойство("ПараметрыОборудования") Тогда
			ВерсияФорматаОбмена = ?(Результат.Свойство("ВерсияФорматаОбмена"), Результат.ВерсияФорматаОбмена, МенеджерОборудованияКлиентПовтИсп.РевизияИнтерфейсаДрайверов());
			РезультатЗавершения = МенеджерОборудованияВызовСервера.СохранитьПараметрыУстройства(Результат.Идентификатор, Результат.ПараметрыОборудования, ВерсияФорматаОбмена);
		КонецЕсли;
		
		Если РезультатЗавершения Тогда 
			ОбновитьПовторноИспользуемыеЗначения();
		Иначе
			СообщениеОбОшибке = НСтр("ru='Не удалось сохранить параметры устройства.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
		КонецЕсли;
		
		Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриЗавершении, РезультатЗавершения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Сохраняет пользовательские настройки подключаемого оборудования.
//
Процедура СохранитьПользовательскиеНастройкиПодключаемогоОборудования(СписокНастроек) Экспорт
	
	МенеджерОборудованияВызовСервера.СохранитьПользовательскиеНастройкиПодключаемогоОборудования(СписокНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

// Открытие формы списка рабочих мест.
//
Процедура ОткрытьРабочиеМеста(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОбновитьРабочееМестоКлиента();
	ПараметрыФормы = Новый Структура();
	ОткрытьФорму("Справочник.РабочиеМеста.ФормаСписка", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

// Процедура для выбора рабочего места текущего сеанса.
//
Процедура ВыбратьРМТекущегоСеанса(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПредложитьВыборРабочегоМестаЗавершение", ЭтотОбъект);
	ПредложитьВыборРабочегоМеста(Оповещение);
	
КонецПроцедуры

// Открытие формы подключаемого Оборудования.
//
Процедура ОткрытьПодключаемоеОборудование(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОбновитьРабочееМестоКлиента();
	
	ПараметрыФормы = Новый Структура();
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.ФормаСписка", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

// Открытие формы драйверов оборудования.
//
Процедура ОткрытьДрайверыОборудования(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОбновитьРабочееМестоКлиента();
	
	ПараметрыФормы = Новый Структура();
	ОткрытьФорму("Справочник.ДрайверыОборудования.ФормаСписка", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

// Функция предоставляет диалог выбора рабочего места.
// 
Процедура ПредложитьВыборРабочегоМеста(ОбработкаОповещения, ИдентификаторКлиента = "") Экспорт

	Результат = Ложь;
	РабочееМесто = "";
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИдентификаторКлиента", ИдентификаторКлиента);
	
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.Форма.ФормаВыбораРабочегоМеста", ПараметрыФормы,,,  ,, ОбработкаОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

// Завершение выбора рабочего места.
//
Процедура ПредложитьВыборРабочегоМестаЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("РабочееМесто") Тогда 
		УстановитьРабочееМесто(Результат.РабочееМесто);
	КонецЕсли;
		
КонецПроцедуры

// Функция устанавливает рабочее место.
// 
Процедура УстановитьРабочееМесто(РабочееМесто) Экспорт
	
	МенеджерОборудованияВызовСервера.УстановитьРабочееМестоКлиента(РабочееМесто);
	Оповестить("ИзмененоРабочееМестоТекущегоСеанса", РабочееМесто);
	
КонецПроцедуры

// Выполняет обновление имени компьютера в параметре сеанса "РабочееМестоКлиента".
//
Функция ОбновитьРабочееМестоКлиента() Экспорт
	
	Результат = Истина;
	
	РабочееМесто = МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента();
	
	Если Не ЗначениеЗаполнено(РабочееМесто) Тогда
		СистемнаяИнформация = Новый СистемнаяИнформация();
		
		МассивРабочихМест = МенеджерОборудованияКлиентПовтИсп.НайтиРабочиеМестаПоИД(ВРег(СистемнаяИнформация.ИдентификаторКлиента));
		Если МассивРабочихМест.Количество() = 0 Тогда
			Параметры = Новый Структура;
			Параметры.Вставить("ИмяКомпьютера");
			Параметры.Вставить("ИдентификаторКлиента");
			
			#Если Не ВебКлиент Тогда
				Параметры.ИмяКомпьютера = ИмяКомпьютера();
			#КонецЕсли
			
			Параметры.ИдентификаторКлиента = ВРег(СистемнаяИнформация.ИдентификаторКлиента);
			РабочееМесто = МенеджерОборудованияВызовСервера.СоздатьРабочееМестоКлиента(Параметры);
		Иначе
			РабочееМесто = МассивРабочихМест[0];
		КонецЕсли;
		
	КонецЕсли;
	
	Если Результат
		И РабочееМесто <> МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента() Тогда
		МенеджерОборудованияВызовСервера.УстановитьРабочееМестоКлиента(РабочееМесто);
		Оповестить("ИзмененоРабочееМестоТекущегоСеанса", РабочееМесто);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет структуру выполнения операции на Оборудовании.
// 
Функция ПараметрыВыполненияОперацииНаОборудовании(Результат = Ложь, ОписаниеОшибки = Неопределено, ИдентификаторУстройства = Неопределено) Экспорт; 
	
	РезультатВыполнения = Новый Структура();
	РезультатВыполнения.Вставить("Результат"              , Результат);
	РезультатВыполнения.Вставить("ОписаниеОшибки"         , ОписаниеОшибки);
	РезультатВыполнения.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	РезультатВыполнения.Вставить("ВыходныеПараметры"      , Неопределено);
	Возврат РезультатВыполнения;
	
КонецФункции

// Процедура выбора устройства из доступных, привязанных к текущему рабочему месту.
//
Процедура ПредложитьВыбратьУстройство(ОповещениеВыбора, ТипОборудования, ТекстЗаголовкаВыбора, 
	СообщениеНеПодключен = "", СообщениеНеВыбран = "", БезСообщений = Ложь, ТекстСообщения = "") Экспорт
	
	Если Не ОбновитьРабочееМестоКлиента() Тогда
		ТекстСообщения = НСтр("ru='Предварительно необходимо выбрать рабочее место подключаемого оборудования текущего сеанса.'");
		Если Не БезСообщений Тогда
		      ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СписокДоступныхУстройств = МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам(ТипОборудования);
	
	Если СписокДоступныхУстройств.Количество() = 0 Тогда
		Если Не ПустаяСтрока(СообщениеНеПодключен) Тогда
			Если БезСообщений Тогда
				ТекстСообщения = СообщениеНеПодключен;
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеНеПодключен);
			КонецЕсли;
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ОповещениеВыбора, Неопределено);
	Иначе
		СписокУстройств = Новый СписокЗначений();
		Для Каждого Устройства Из СписокДоступныхУстройств Цикл
			СписокУстройств.Добавить(Устройства.Ссылка, Устройства.Наименование);
		КонецЦикла;
		Если СписокУстройств.Количество() = 1 Тогда
			Идентификатор = СписокУстройств[0].Значение;
			ВыполнитьОбработкуОповещения(ОповещениеВыбора, Идентификатор); 
		Иначе
			Контекст = Новый Структура;
			Контекст.Вставить("СледующееОповещение", ОповещениеВыбора);
			Контекст.Вставить("СообщениеНеВыбран"  , ?(ПустаяСтрока(СообщениеНеВыбран), СообщениеНеПодключен, СообщениеНеВыбран));
			Контекст.Вставить("БезСообщений"       , БезСообщений);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПредложитьВыбратьУстройствоЗавершение", ЭтотОбъект, Контекст);
			СписокУстройств.ПоказатьВыборЭлемента(ОписаниеОповещения, ТекстЗаголовкаВыбора);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПредложитьВыбратьУстройствоЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено И НЕ Параметры.БезСообщений И Не ПустаяСтрока(Параметры.СообщениеНеВыбран) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Параметры.СообщениеНеВыбран);
	КонецЕсли;
	
	Если Параметры.СледующееОповещение <> Неопределено Тогда
		Идентификатор = ?(Результат = Неопределено, Неопределено, Результат.Значение);
		ВыполнитьОбработкуОповещения(Параметры.СледующееОповещение, Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

// Функция начинает выбор файла драйвера для последующей загрузки.
//
Процедура НачатьВыборФайлаДрайвера(ОповещениеПриВыборе) Экспорт 
	
	Результат = Ложь;
	ПолноеИмяФайла = "";
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru='Выберите файл драйвера'");
	ДиалогОткрытияФайла.Фильтр = НСтр("ru='Файл драйвера'") + ?(МенеджерОборудованияКлиентПовтИсп.ЭтоLinuxКлиент(), "(*.zip)|*.zip", "(*.zip, *.exe)| *.zip; *.exe");  
	
	Параметры = Новый Структура("СледующееОповещение", ОповещениеПриВыборе);
	Оповещение = Новый ОписаниеОповещения("ВыборФайлаДрайвераЗавершение", ЭтотОбъект, Параметры);
	
	ДиалогОткрытияФайла.Показать(Оповещение);
	
КонецПроцедуры

// Завершение выбора файла драйвера.
//
Процедура ВыборФайлаДрайвераЗавершение(ВыбранныеФайлы, Параметры) Экспорт
	
	Если ТипЗнч(ВыбранныеФайлы) = Тип("Массив") И ВыбранныеФайлы.Количество() > 0  Тогда
		Если Параметры.СледующееОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(Параметры.СледующееОповещение, ВыбранныеФайлы[0]);
		КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

// Завершение выбора файла
//
Процедура НачатьВыборФайлаРасширенияЗавершение(Установлено, ДополнительныеПараметры) Экспорт
	
	Если Установлено Тогда
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла[ДополнительныеПараметры.РежимДиалогаВыбораФайла]);
		Диалог.МножественныйВыбор = Ложь;
		Диалог.ПолноеИмяФайла = ДополнительныеПараметры.ИмяФайла;
		Диалог.Показать(ДополнительныеПараметры.ОповещениеПриВыборе);
	КонецЕсли;
	
КонецПроцедуры
 
// Функция начинает выбор файла.
//
Процедура НачатьВыборФайла(ОповещениеПриВыборе, Знач ИмяФайла, РежимДиалогаВыбораФайла = "Открытие") Экспорт
	
	ПараметрыКоманды = Новый Структура("ОповещениеПриВыборе, ИмяФайла, РежимДиалогаВыбораФайла", ОповещениеПриВыборе, ИмяФайла, РежимДиалогаВыбораФайла);
	Оповещение = Новый ОписаниеОповещения("НачатьВыборФайлаРасширенияЗавершение", ЭтотОбъект, ПараметрыКоманды);
	ПроверитьДоступностьРасширенияРаботыСФайлами(Оповещение);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// Процедура формирует задержку указанной длительности.
//
// Параметры:
//  Время - <Число>
//        - Длительность задержки в секундах.
//
Процедура Пауза(Время) Экспорт

	ВремяЗавершения = ТекущаяДата() + Время;
	Пока ТекущаяДата() < ВремяЗавершения Цикл
	КонецЦикла;

КонецПроцедуры

// Преобразовать список строкой в массив.
//
Функция ПреобразоватьСписокСтрокойВМассив(Источник) Экспорт
	
	ПромежуточнаяСтруктура = Новый Структура(Источник);
	Приемник = Новый Массив;
	
	Для Каждого КлючИЗначение Из ПромежуточнаяСтруктура Цикл
		Приемник.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	Возврат Приемник;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииПодключениеОборудованияАсинхронно

// Производит подключение доступного оборудования по списку типов ПО
//
Процедура НачатьПодключениеОборудованиеПоТипу(ОповещениеПриПодключении, ИдентификаторКлиента, ТипыПО) Экспорт
	
	СтруктураТиповПО = Новый Структура();
	Если ТипЗнч(ТипыПО) = Тип("Массив") Тогда
		Для Каждого ТипПО Из ТипыПО Цикл
			СтруктураТиповПО.Вставить(ТипПО);
		КонецЦикла;
	Иначе
		СтруктураТиповПО.Вставить(ТипыПО);
	КонецЕсли;
	
	НачатьПодключениеОборудования(ОповещениеПриПодключении, ИдентификаторКлиента, СтруктураТиповПО);
	 
 КонецПроцедуры

// Начать подключать одиночный экземпляр устройства определяемый идентификатором.
//
Процедура НачатьПодключениеОборудованиеПоИдентификатору(ОповещениеПриПодключении, ИдентификаторКлиента, ИдентификаторУстройства) Экспорт
	
	НачатьПодключениеОборудования(ОповещениеПриПодключении, ИдентификаторКлиента, , ИдентификаторУстройства);
	
КонецПроцедуры

// Начать подключения устройства.
// 
Процедура НачатьПодключениеОборудования(ОповещениеПриПодключении, ИдентификаторКлиента, ТипыПО = Неопределено, ИдентификаторУстройства = Неопределено) Экспорт
	   
	ОбъектДрайвера = Неопределено;
	
	Результат = ОбновитьРабочееМестоКлиента();
	Если Не Результат Тогда
		Если ОповещениеПриПодключении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Предварительно необходимо выбрать рабочее место подключаемого оборудования текущего сеанса.'");
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Результат, ОписаниеОшибки);
			ВыполнитьОбработкуОповещения(ОповещениеПриПодключении, РезультатВыполнения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СписокОборудования = МенеджерОборудованияКлиентПовтИсп.ОборудованиеПоПараметрам(ТипыПО, ИдентификаторУстройства);
	
	Если СписокОборудования.Количество() > 0 Тогда
		Для Каждого Устройство Из СписокОборудования Цикл
			
			// Проверим, не подключено ли устройство ранее.
			ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Устройство.Ссылка);
			
			Если ПодключенноеУстройство = Неопределено Тогда // Если устройство не было подключено ранее.
				
				ОбработчикДрайвераМодуль = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(Устройство.ОбработчикДрайвера, Не Устройство.ВСоставеКонфигурации, Устройство.ТипОборудованияИмя);
				
				НовоеПодключение = Новый Структура();
				НовоеПодключение.Вставить("Клиенты"                 , Новый Массив());
				НовоеПодключение.Клиенты.Добавить(ИдентификаторКлиента);
				НовоеПодключение.Вставить("Ссылка"                  , Устройство.Ссылка);
				НовоеПодключение.Вставить("ИдентификаторУстройства" , Устройство.ИдентификаторУстройства);
				НовоеПодключение.Вставить("ОбработчикДрайвера"      , Устройство.ОбработчикДрайвера);
				НовоеПодключение.Вставить("Наименование"            , Устройство.Наименование);
				НовоеПодключение.Вставить("ТипОборудования"         , Устройство.ТипОборудования);
				НовоеПодключение.Вставить("ТипОборудованияИмя"      , Устройство.ТипОборудованияИмя);
				НовоеПодключение.Вставить("ДрайверОборудования"     , Устройство.ДрайверОборудования);
				НовоеПодключение.Вставить("ВСоставеКонфигурации"    , Устройство.ВСоставеКонфигурации);
				НовоеПодключение.Вставить("ИдентификаторОбъекта"    , Устройство.ИдентификаторОбъекта);
				НовоеПодключение.Вставить("ИмяМакетаДрайвера"       , Устройство.ИмяМакетаДрайвера);
				НовоеПодключение.Вставить("ИмяФайлаДрайвера"        , Устройство.ИмяФайлаДрайвера);
				НовоеПодключение.Вставить("ВерсияФорматаОбмена"     , Устройство.ВерсияФорматаОбмена);
				НовоеПодключение.Вставить("РабочееМесто"            , Устройство.РабочееМесто);
				НовоеПодключение.Вставить("ИмяКомпьютера"           , Устройство.ИмяКомпьютера);
				НовоеПодключение.Вставить("Параметры"               , Устройство.Параметры);
				НовоеПодключение.Вставить("ПараметрыРегистрации"    , Устройство.ПараметрыРегистрации);
				НовоеПодключение.Вставить("КоличествоПодключенных"  , 1);
				НовоеПодключение.Вставить("ПараметрыПодключения"    , Новый Структура());
				НовоеПодключение.Вставить("ОбъектДрайвера"          , Неопределено);
				НовоеПодключение.Вставить("ОбработчикДрайвераМодуль", ОбработчикДрайвераМодуль);
				НовоеПодключение.ПараметрыПодключения.Вставить("ТипОборудования"     , Устройство.ТипОборудованияИмя);
				НовоеПодключение.ПараметрыПодключения.Вставить("ВерсияФорматаОбмена" , Устройство.ВерсияФорматаОбмена);
				НовоеПодключение.ПараметрыПодключения.Вставить("ПараметрыРегистрации", Устройство.ПараметрыРегистрации);
				
				Если ОбработчикДрайвераМодуль = Неопределено Тогда
					// Сообщить об ошибке, что не удалось подключить обработчик.
					Если ОповещениеПриПодключении <> Неопределено Тогда
						ОписаниеОшибки = НСтр("ru='Не удалось подключить обработчик драйвера.'");
						РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
						ВыполнитьОбработкуОповещения(ОповещениеПриПодключении, РезультатВыполнения);
					КонецЕсли;
					Продолжить;
				Иначе
					
					// Разделение на асинхронные и синхронные вызовы.
					Если ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима() Тогда
						// Асинхронные вызовы
						ПараметрыКоманды = Новый Структура("НовоеПодключение, ОповещениеПриПодключении, ОбработчикДрайвераМодуль", НовоеПодключение, ОповещениеПриПодключении, ОбработчикДрайвераМодуль);
						Оповещение = Новый ОписаниеОповещения("НачатьПодключениеОборудования_ПолучениеОбъектаДрайвераЗавершение", ЭтотОбъект, ПараметрыКоманды);
						НачатьПолучениеОбъектаДрайвера(Оповещение, Устройство);
					Иначе
						// Синхронные
						ОбъектДрайвера = ПолучитьОбъектДрайвера(Устройство);
						Если ОбъектДрайвера = Неопределено Тогда
							Если ОповещениеПриПодключении <> Неопределено Тогда
								// Сообщить об ошибке, что не удалось загрузить драйвер.
								ОписаниеОшибки = НСтр("ru='%Наименование%: Не удалось загрузить драйвер устройства.
													|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
								ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%",НовоеПодключение.Наименование);
								РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
								ВыполнитьОбработкуОповещения(ОповещениеПриПодключении, РезультатВыполнения);
							КонецЕсли;
							Продолжить;
						Иначе
							ВыходныеПараметры = Неопределено;
							Результат = ОбработчикДрайвераМодуль.ПодключитьУстройство(ОбъектДрайвера, НовоеПодключение.Параметры, НовоеПодключение.ПараметрыПодключения, ВыходныеПараметры);
							
							Если Результат Тогда
								
								Если ВыходныеПараметры.Количество() >= 2 Тогда
									НовоеПодключение.Вставить("ИсточникСобытия", ВыходныеПараметры[0]);
									НовоеПодключение.Вставить("ИменаСобытий",    ВыходныеПараметры[1]);
								Иначе
									НовоеПодключение.Вставить("ИсточникСобытия", "");
									НовоеПодключение.Вставить("ИменаСобытий",    Неопределено);
								КонецЕсли;
								НовоеПодключение.ОбъектДрайвера = ОбъектДрайвера;
								глПодключаемоеОборудование.ПараметрыПодключенияПО.Добавить(НовоеПодключение);
								
								Если ОповещениеПриПодключении <> Неопределено Тогда
									ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
									РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ПараметрыПодключения", Истина, ОписаниеОшибки, НовоеПодключение.ПараметрыПодключения);
									ВыполнитьОбработкуОповещения(ОповещениеПриПодключении, РезультатВыполнения);
								КонецЕсли;
								
							Иначе
								// Сообщим пользователю о том, что не удалось подключить устройство.
								Если ОповещениеПриПодключении <> Неопределено Тогда
									ОписаниеОшибки = НСтр("ru='Не удалось подключить устройство ""%Наименование%"": %ОписаниеОшибки% (%КодОшибки%)'");
									ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%"  , НовоеПодключение.Наименование);
									ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", ВыходныеПараметры[1]);
									ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%КодОшибки%"     , ВыходныеПараметры[0]);
									РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
									ВыполнитьОбработкуОповещения(ОповещениеПриПодключении, РезультатВыполнения);
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
			
			Иначе // Устройство было подключено ранее.
				// Увеличим количество пользователей данного соединения.
				ПодключенноеУстройство.Клиенты.Добавить(ИдентификаторКлиента);
				ПодключенноеУстройство.КоличествоПодключенных = ПодключенноеУстройство.КоличествоПодключенных + 1;
				Если ОповещениеПриПодключении <> Неопределено Тогда
					ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
					РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ПараметрыПодключения", Истина, ОписаниеОшибки, ПодключенноеУстройство.ПараметрыПодключения);
					ВыполнитьОбработкуОповещения(ОповещениеПриПодключении, РезультатВыполнения);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ИдентификаторУстройства <> Неопределено И ОповещениеПриПодключении <> Неопределено Тогда
		ОписаниеОшибки =  НСтр("ru='Выбранное устройство не может использоваться для подключения. Укажите другое устройство.'");
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
		ВыполнитьОбработкуОповещения(ОповещениеПриПодключении, РезультатВыполнения);
	ИначеЕсли ТипыПО = Неопределено И ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОшибки =  НСтр("ru='Нет доступного оборудования для подключения.'");
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
		ВыполнитьОбработкуОповещения(ОповещениеПриПодключении, РезультатВыполнения);
	КонецЕсли;

КонецПроцедуры

Процедура НачатьПодключениеОборудования_ПолучениеОбъектаДрайвераЗавершение(ОбъектДрайвера, Параметры) Экспорт
	
	Если ОбъектДрайвера = Неопределено Тогда
		
		Если Параметры.ОповещениеПриПодключении <> Неопределено Тогда
			// Сообщить об ошибке, что не удалось загрузить драйвер.
			ОписаниеОшибки = НСтр("ru='%Наименование%: Не удалось загрузить драйвер устройства.
										|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", Параметры.НовоеПодключение.Наименование);
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриПодключении, РезультатВыполнения);
		КонецЕсли;
		
	Иначе
		Параметры.НовоеПодключение.ОбъектДрайвера = ОбъектДрайвера;
		Оповещение = Новый ОписаниеОповещения("НачатьПодключениеОборудования_Завершение", ЭтотОбъект, Параметры);
		Параметры.ОбработчикДрайвераМодуль.НачатьПодключениеУстройства(Оповещение, ОбъектДрайвера, 
			 Параметры.НовоеПодключение.Параметры,  Параметры.НовоеПодключение.ПараметрыПодключения, Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодключениеОборудования_Завершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		Если РезультатВыполнения.ВыходныеПараметры.Количество() >= 2 Тогда
			Параметры.НовоеПодключение.Вставить("ИсточникСобытия", Параметры.ВыходныеПараметры[0]);
			Параметры.НовоеПодключение.Вставить("ИменаСобытий",    Параметры.ВыходныеПараметры[1]);
		Иначе
			Параметры.НовоеПодключение.Вставить("ИсточникСобытия", "");
			Параметры.НовоеПодключение.Вставить("ИменаСобытий",    Неопределено);
		КонецЕсли;
		глПодключаемоеОборудование.ПараметрыПодключенияПО.Добавить(Параметры.НовоеПодключение);
		Если Параметры.ОповещениеПриПодключении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
			РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки, ПараметрыПодключения", Истина, ОписаниеОшибки, Параметры.НовоеПодключение.ПараметрыПодключения);
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриПодключении, РезультатВыполнения);
		КонецЕсли;
	Иначе
		// Сообщим пользователю о том, что не удалось подключить устройство.
		Если Параметры.ОповещениеПриПодключении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Не удалось подключить устройство ""%Наименование%"": %ОписаниеОшибки%'");
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%"  , Параметры.НовоеПодключение.Наименование);
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", РезультатВыполнения.ВыходныеПараметры[1]);
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриПодключении, РезультатВыполнения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Начать отключение устройств по типу оборудования.
//
Процедура НачатьОтключениеОборудованиеПоТипу(ОповещениеПриОтключении, ИдентификаторКлиента, ТипыПО) Экспорт
	
	НачатьОтключениеОборудование(ОповещениеПриОтключении, ИдентификаторКлиента, ТипыПО, );
	
КонецПроцедуры

// Начать отключать устройства определенное идентификатором.
//
Процедура НачатьОтключениеОборудованиеПоИдентификатору(ОповещениеПриОтключении, ИдентификаторКлиента, ИдентификаторУстройства) Экспорт
	
	НачатьОтключениеОборудование(ОповещениеПриОтключении, ИдентификаторКлиента, , ИдентификаторУстройства);
	
КонецПроцедуры

// Функция подключает устройства по типу оборудования.
// 
Процедура НачатьОтключениеОборудование(ОповещениеПриОтключении, ИдентификаторКлиента, ТипыПО = Неопределено, ИдентификаторУстройства = Неопределено)
	
	Если глПодключаемоеОборудование.ПараметрыПодключенияПО <> Неопределено Тогда
		КоличествоУстройств = глПодключаемоеОборудование.ПараметрыПодключенияПО.Количество();
		Для Индекс = 1 По КоличествоУстройств Цикл
			
			ПодключенноеУстройство = глПодключаемоеОборудование.ПараметрыПодключенияПО[КоличествоУстройств - Индекс];
			КлиентПодключения = ПодключенноеУстройство.Клиенты.Найти(ИдентификаторКлиента);
			
			Если КлиентПодключения <> Неопределено  И (ТипыПО = Неопределено Или ТипыПО.Найти(ПодключенноеУстройство.ТипОборудованияИмя) <> Неопределено)
			   И (ИдентификаторУстройства = Неопределено  Или ПодключенноеУстройство.Ссылка = ИдентификаторУстройства) Тогда
				
				Если ПодключенноеУстройство.КоличествоПодключенных = 1 Тогда
					
					ОбработчикДрайвераМодуль = ПодключенноеУстройство.ОбработчикДрайвераМодуль;
					ОбъектДрайвера = ПодключенноеУстройство.ОбъектДрайвера;
					
					Если ОбработчикДрайвераМодуль = Неопределено Тогда
						// Сообщить об ошибке, что не удалось подключить обработчик.
						Если ОповещениеПриОтключении <> Неопределено Тогда
							ОписаниеОшибки = НСтр("ru='Не удалось подключить обработчик драйвера.'");
							РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
							ВыполнитьОбработкуОповещения(ОповещениеПриОтключении, РезультатВыполнения);
						КонецЕсли;
					Иначе
						
						Если ОбъектДрайвера = Неопределено Тогда
							Если ОповещениеПриОтключении <> Неопределено Тогда
								// Сообщить об ошибке, что не удалось загрузить драйвер.
								ОписаниеОшибки = НСтр("ru='%Наименование%: Не удалось загрузить драйвер устройства.
													|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
								ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", ПодключенноеУстройство.Наименование);
								РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
								ВыполнитьОбработкуОповещения(ОповещениеПриОтключении, РезультатВыполнения);
							КонецЕсли;
						Иначе
							
							ВыходныеПараметры = Неопределено;
							           
							// Разделение на асинхронные и синхронные вызовы.
							Если ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима() Тогда
								
								ПараметрыКоманды = Новый Структура("ПодключенноеУстройство, ОповещениеПриОтключении", ПодключенноеУстройство, ОповещениеПриОтключении);
								Оповещение = Новый ОписаниеОповещения("НачатьОтключениеОборудование_Завершение", ЭтотОбъект, ПараметрыКоманды);
								ОбработчикДрайвераМодуль.НачатьОтключениеУстройства(Оповещение, ОбъектДрайвера, 
									ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения, ВыходныеПараметры);
							Иначе
								Результат = ОбработчикДрайвераМодуль.ОтключитьУстройство(ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения, ВыходныеПараметры);
								Если НЕ Результат Тогда
									// Сообщим пользователю о том, что не удалось подключить устройство.
									Если ОповещениеПриОтключении <> Неопределено Тогда
										ОписаниеОшибки = НСтр("ru='При отключении устройства ""%Наименование%"" произошла ошибка: %ОписаниеОшибки%'");
										ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%"  , ПодключенноеУстройство.Наименование);
										ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", ВыходныеПараметры[1]);
										РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
										ВыполнитьОбработкуОповещения(ОповещениеПриОтключении, РезультатВыполнения);
									КонецЕсли;
								Иначе
									ПодключенноеУстройство.КоличествоПодключенных = 0;
								КонецЕсли;
								
								НомерСтрокиМассива = глПодключаемоеОборудование.ПараметрыПодключенияПО.Найти(ПодключенноеУстройство);
								Если НомерСтрокиМассива <> Неопределено Тогда
									глПодключаемоеОборудование.ПараметрыПодключенияПО.Удалить(НомерСтрокиМассива);
								КонецЕсли;
								
								Если ОповещениеПриОтключении <> Неопределено Тогда
									ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
									РезультатВыполнения = Новый Структура("Результат, ОписаниеОшибки", Истина, ОписаниеОшибки);
									ВыполнитьОбработкуОповещения(ОповещениеПриОтключении, РезультатВыполнения);
								КонецЕсли;
							КонецЕсли;
							
						КонецЕсли;
					КонецЕсли;
				Иначе
					ПодключенноеУстройство.КоличествоПодключенных = ПодключенноеУстройство.КоличествоПодключенных - 1;
					ПодключенноеУстройство.Клиенты.Удалить(КлиентПодключения);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры  

Процедура НачатьОтключениеОборудование_Завершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		Параметры.ПодключенноеУстройство.КоличествоПодключенных = 0;
		
		НомерСтрокиМассива = глПодключаемоеОборудование.ПараметрыПодключенияПО.Найти(Параметры.ПодключенноеУстройство);
		Если НомерСтрокиМассива <> Неопределено Тогда
			глПодключаемоеОборудование.ПараметрыПодключенияПО.Удалить(НомерСтрокиМассива);
		КонецЕсли;
		Если Параметры.ОповещениеПриОтключении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='Ошибок нет.'");
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Истина, ОписаниеОшибки);
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриОтключении, РезультатВыполнения);
		КонецЕсли;
	Иначе
		// Сообщим пользователю о том, что не удалось подключить устройство.
		Если Параметры.ОповещениеПриОтключении <> Неопределено Тогда
			ОписаниеОшибки = НСтр("ru='При отключении устройства ""%Наименование%"" произошла ошибка.'");
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%"  , Параметры.ПодключенноеУстройство.Наименование);
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ОписаниеОшибки);
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриОтключении, РезультатВыполнения);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Производит принудительное отключение всего подключенного оборудования,
// независимо от числа ссылок на подключение.
Процедура НачатьОтключениеВсегоОборудования(ОповещениеПриОтключении = Неопределено) Экспорт
	
	КонечныйРезультат = Истина;
	Результат         = Истина;
	
	Для Каждого ПодключенноеУстройство Из глПодключаемоеОборудование.ПараметрыПодключенияПО Цикл
		
		Если ПодключенноеУстройство.ОбъектДрайвера = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ВыходныеПараметры = Неопределено;
		ОбработчикДрайвераМодуль = ПодключенноеУстройство.ОбработчикДрайвераМодуль;
		Если ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима() Тогда
			Оповещение = Новый ОписаниеОповещения("НачатьОтключениеВсегоОборудованияЗавершение", МенеджерОборудованияКлиент);
			ОбработчикДрайвераМодуль.НачатьОтключениеУстройства(Оповещение, ПодключенноеУстройство.ОбъектДрайвера, ПодключенноеУстройство.Параметры, 
				ПодключенноеУстройство.ПараметрыПодключения, ВыходныеПараметры);
			ПодключенноеУстройство.КоличествоПодключенных = 0;
			Результат = Истина;
		Иначе
			Результат = ОбработчикДрайвераМодуль.ОтключитьУстройство(ПодключенноеУстройство.ОбъектДрайвера, ПодключенноеУстройство.Параметры, 
				ПодключенноеУстройство.ПараметрыПодключения, ВыходныеПараметры);
			Если Результат Тогда
				ПодключенноеУстройство.КоличествоПодключенных = 0;
			КонецЕсли;
		КонецЕсли;
		КонечныйРезультат = КонечныйРезультат И Результат;
		
	КонецЦикла;
	
	КоличествоПодключенныхКлиентов = 0;
	Для Каждого ПодключенноеУстройство Из глПодключаемоеОборудование.ПараметрыПодключенияПО Цикл
		КоличествоПодключенныхКлиентов = КоличествоПодключенныхКлиентов + ПодключенноеУстройство.КоличествоПодключенных;
	КонецЦикла;
	
	глПодключаемоеОборудование.ПараметрыПодключенияПО.Очистить();
	
КонецПроцедуры

Процедура НачатьОтключениеВсегоОборудованияЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru='При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти  

#Область ПроцедурыИФункцииПодключениеОборудованияСинтронноУстарели

// Подключает одиночный экземпляр устройства определяемый идентификатором.
//
Функция ПодключитьОборудованиеПоИдентификатору(ИдентификаторКлиента, ИдентификаторУстройства, ОписаниеОшибки = "")
	
	Возврат ПодключитьОборудование(ИдентификаторКлиента, , ИдентификаторУстройства, ОписаниеОшибки);
	
КонецФункции

// Функция подключает устройства по типу оборудования.
// Возвращает результат выполнения функции.
//
Функция ПодключитьОборудование(ИдентификаторКлиента, ТипыПО = Неопределено, ИдентификаторУстройства = Неопределено, ОписаниеОшибки = "")
	   
	КонечныйРезультат = Истина;
	Результат         = Истина;
	
	ОбъектДрайвера    = Неопределено;
	ОписаниеОшибки    = "";
	ОписаниеОшибкиУстройства = "";

	Результат = ОбновитьРабочееМестоКлиента();
	Если Не Результат Тогда
		ОписаниеОшибки = НСтр("ru='Предварительно необходимо выбрать рабочее место подключаемого оборудования текущего сеанса.'");
		Возврат Ложь;
	КонецЕсли;
	
	СписокОборудования = МенеджерОборудованияКлиентПовтИсп.ОборудованиеПоПараметрам(ТипыПО, ИдентификаторУстройства);
	
	Если СписокОборудования.Количество() > 0 Тогда
		Для Каждого Устройство Из СписокОборудования Цикл
			
			// Проверим, не подключено ли устройство ранее.
			ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Устройство.Ссылка);
			
			Если ПодключенноеУстройство = Неопределено Тогда // Если устройство не было подключено ранее.
				ОбъектДрайвера = ПолучитьОбъектДрайвера(Устройство);
				Если ОбъектДрайвера = Неопределено Тогда
					// Сообщить об ошибке, что не удалось загрузить драйвер.
					ОписаниеОшибки = ОписаниеОшибки + ?(ПустаяСтрока(ОписаниеОшибки), "", Символы.ПС)
								   + НСтр("ru='%Наименование%: Не удалось загрузить драйвер устройства.
									 |Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
					ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", Устройство.Наименование);
					КонечныйРезультат = Ложь;
					Продолжить;
				КонецЕсли;
				
				НовоеПодключение = Новый Структура();
				НовоеПодключение.Вставить("Клиенты"               , Новый Массив());
				НовоеПодключение.Клиенты.Добавить(ИдентификаторКлиента);
				НовоеПодключение.Вставить("Ссылка"                  , Устройство.Ссылка);
				НовоеПодключение.Вставить("ИдентификаторУстройства" , Устройство.ИдентификаторУстройства);
				НовоеПодключение.Вставить("ОбработчикДрайвера"      , Устройство.ОбработчикДрайвера);
				НовоеПодключение.Вставить("Наименование"            , Устройство.Наименование);
				НовоеПодключение.Вставить("ТипОборудования"         , Устройство.ТипОборудования);
				НовоеПодключение.Вставить("ТипОборудованияИмя"      , Устройство.ТипОборудованияИмя);
				НовоеПодключение.Вставить("ДрайверОборудования"     , Устройство.ДрайверОборудования);
				НовоеПодключение.Вставить("ВСоставеКонфигурации"    , Устройство.ВСоставеКонфигурации);
				НовоеПодключение.Вставить("ИдентификаторОбъекта"    , Устройство.ИдентификаторОбъекта);
				НовоеПодключение.Вставить("ИмяМакетаДрайвера"       , Устройство.ИмяМакетаДрайвера);
				НовоеПодключение.Вставить("ИмяФайлаДрайвера"        , Устройство.ИмяФайлаДрайвера);
				НовоеПодключение.Вставить("ВерсияФорматаОбмена"     , Устройство.ВерсияФорматаОбмена);
				НовоеПодключение.Вставить("РабочееМесто"            , Устройство.РабочееМесто);
				НовоеПодключение.Вставить("ИмяКомпьютера"           , Устройство.ИмяКомпьютера);
				НовоеПодключение.Вставить("Параметры"               , Устройство.Параметры);
				НовоеПодключение.Вставить("КоличествоПодключенных"  , 1);
				НовоеПодключение.Вставить("ПараметрыПодключения"    , Новый Структура());
				НовоеПодключение.ПараметрыПодключения.Вставить("ТипОборудования", Устройство.ТипОборудованияИмя);
				НовоеПодключение.ПараметрыПодключения.Вставить("ВерсияФорматаОбмена", Устройство.ВерсияФорматаОбмена);
				
				ВыходныеПараметры = Неопределено;
				   
				ОбработчикДрайвераМодуль = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(НовоеПодключение.ОбработчикДрайвера, Не НовоеПодключение.ВСоставеКонфигурации, Устройство.ТипОборудованияИмя);
				
				Если ОбработчикДрайвераМодуль = Неопределено Тогда
					// Сообщить об ошибке, что не удалось загрузить драйвер.
					ОписаниеОшибки = ОписаниеОшибки +  НСтр("ru='Не удалось подключить обработчик драйвера.'");
					КонечныйРезультат = Ложь;
					Продолжить;
				Иначе
					#Если ВебКлиент Тогда
					// Принудительно заменяем асинхронный обработчик на синхронный, так как работам в синхронном режиме.
					Если ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверАсинхронноКлиент Тогда
						ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент;
					КонецЕсли;
					#КонецЕсли
					Результат = ОбработчикДрайвераМодуль.ПодключитьУстройство(ОбъектДрайвера, НовоеПодключение.Параметры, 
						НовоеПодключение.ПараметрыПодключения, ВыходныеПараметры);
				КонецЕсли;
				
				Если Результат Тогда
					Если ВыходныеПараметры.Количество() >= 2 Тогда
						НовоеПодключение.Вставить("ИсточникСобытия", ВыходныеПараметры[0]);
						НовоеПодключение.Вставить("ИменаСобытий",    ВыходныеПараметры[1]);
					Иначе
						НовоеПодключение.Вставить("ИсточникСобытия", "");
						НовоеПодключение.Вставить("ИменаСобытий",    Неопределено);
					КонецЕсли;
					НовоеПодключение.Вставить("ОбъектДрайвера"          , ОбъектДрайвера);
					НовоеПодключение.Вставить("ОбработчикДрайвераМодуль", ОбработчикДрайвераМодуль);
					глПодключаемоеОборудование.ПараметрыПодключенияПО.Добавить(НовоеПодключение);
				Иначе
					// Сообщим пользователю о том, что не удалось подключить устройство.
					ОписаниеОшибки = ОписаниеОшибки + ?(ПустаяСтрока(ОписаниеОшибки), "", Символы.ПС)
								   + НСтр("ru='Не удалось подключить устройство ""%Наименование%"": %ОписаниеОшибки% (%КодОшибки%)'");
					ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%"  , Устройство.Наименование);
					ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", ВыходныеПараметры[1]);
					ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%КодОшибки%"     , ВыходныеПараметры[0]);
				КонецЕсли;
			Иначе // Устройство было подключено ранее.
				// Увеличим количество пользователей данного соединения.
				ПодключенноеУстройство.Клиенты.Добавить(ИдентификаторКлиента);
				ПодключенноеУстройство.КоличествоПодключенных = ПодключенноеУстройство.КоличествоПодключенных + 1;
			КонецЕсли;
			
			КонечныйРезультат = КонечныйРезультат И Результат;
		КонецЦикла;
		
	ИначеЕсли ИдентификаторУстройства <> Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Выбранное устройство не может использоваться для подключения.
		|Укажите другое устройство.'");
		КонечныйРезультат = Ложь;
	ИначеЕсли ТипыПО = Неопределено И ИдентификаторУстройства = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru='Нет доступного оборудования для подключения.'");
		КонечныйРезультат = Ложь;
	КонецЕсли;
	
	Возврат КонечныйРезультат;
	
КонецФункции

// Отключает устройство, определенное идентификатором.
//
Функция ОтключитьОборудованиеПоИдентификатору(ИдентификаторКлиента, ИдентификаторУстройства, ОписаниеОшибки = "") 
	
	Возврат ОтключитьОборудование(ИдентификаторКлиента, , ИдентификаторУстройства, ОписаниеОшибки);
	
КонецФункции

// Функция подключает устройства по типу оборудования.
// 
Функция ОтключитьОборудование(ИдентификаторКлиента, ТипыПО = Неопределено, ИдентификаторУстройства = Неопределено, ОписаниеОшибки = "")
	
	КонечныйРезультат = Истина;
	Результат         = Истина;
	
	ВыходноеОписаниеОшибки = "";
	
	Если глПодключаемоеОборудование.ПараметрыПодключенияПО <> Неопределено Тогда
		КоличествоУстройств = глПодключаемоеОборудование.ПараметрыПодключенияПО.Количество();
		Для Индекс = 1 По КоличествоУстройств Цикл
			
			ПодключенноеУстройство = глПодключаемоеОборудование.ПараметрыПодключенияПО[КоличествоУстройств - Индекс];
			КлиентПодключения = ПодключенноеУстройство.Клиенты.Найти(ИдентификаторКлиента);
			
			Если КлиентПодключения <> Неопределено  И (ТипыПО = Неопределено Или ТипыПО.Найти(ПодключенноеУстройство.ТипОборудованияИмя) <> Неопределено)
			   И (ИдентификаторУстройства = Неопределено  Или ПодключенноеУстройство.Ссылка = ИдентификаторУстройства)Тогда
				 
				 Если ПодключенноеУстройство.КоличествоПодключенных = 1 Тогда
					 
					Если ПодключенноеУстройство.ОбъектДрайвера = Неопределено Тогда
						// Сообщить об ошибке, что не удалось загрузить драйвер.
						ОписаниеОшибки = НСтр("ru='""%Наименование%"": Не удалось загрузить драйвер устройства.
													|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
						ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", ПодключенноеУстройство.Наименование);
						КонечныйРезультат = Ложь;
						Продолжить;
					КонецЕсли;
					
					ОбработчикДрайвераМодуль = ПодключенноеУстройство.ОбработчикДрайвераМодуль;
					#Если ВебКлиент Тогда
					// Принудительно заменяем асинхронный обработчик на синхронный, так как работам в синхронном режиме.
					Если ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверАсинхронноКлиент Тогда
						ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент;
					КонецЕсли;
					#КонецЕсли
					
					ВыходныеПараметры = Неопределено;
					Результат = ОбработчикДрайвераМодуль.ОтключитьУстройство(ПодключенноеУстройство.ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения, ВыходныеПараметры);
					Если НЕ Результат Тогда
						ОписаниеОшибки = ОписаниеОшибки + ?(ПустаяСтрока(ОписаниеОшибки), "", Символы.ПС)
									   + НСтр("ru='При отключении устройства ""%Наименование%"" произошла ошибка: %ОписаниеОшибки% (%КодОшибки%)'");
						ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", ПодключенноеУстройство.Наименование);
						ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ОписаниеОшибки%", ВыходныеПараметры[1]);
						ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%КодОшибки%", ВыходныеПараметры[0]);
					Иначе
						ПодключенноеУстройство.КоличествоПодключенных = 0;
					КонецЕсли;
					
					НомерСтрокиМассива = глПодключаемоеОборудование.ПараметрыПодключенияПО.Найти(ПодключенноеУстройство);
					Если НомерСтрокиМассива <> Неопределено Тогда
						глПодключаемоеОборудование.ПараметрыПодключенияПО.Удалить(НомерСтрокиМассива);
					КонецЕсли;
				Иначе
					ПодключенноеУстройство.КоличествоПодключенных = ПодключенноеУстройство.КоличествоПодключенных - 1;
					ПодключенноеУстройство.Клиенты.Удалить(КлиентПодключения);
				КонецЕсли;
			КонецЕсли;
			
			КонечныйРезультат = КонечныйРезультат И Результат;
		КонецЦикла;
	КонецЕсли;
	
	Возврат КонечныйРезультат;
	
КонецФункции  

Функция ВыполнитьКоманду(Идентификатор, Команда, ВходныеПараметры, ВыходныеПараметры, Таймаут = -1) Экспорт
	
	Результат = Ложь;
	
	// Поиск подключенного устройства.
	ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Идентификатор);
	
	Если ПодключенноеУстройство <> Неопределено Тогда
		
		ОбработчикДрайвераМодуль = ПодключенноеУстройство.ОбработчикДрайвераМодуль;
		ОбъектДрайвера = ПодключенноеУстройство.ОбъектДрайвера;
		
		Если ОбъектДрайвера = Неопределено Тогда
			// Сообщить об ошибке, что не удалось загрузить драйвер.
			ОписаниеОшибки = НСтр("ru='""%Наименование%"": Не удалось загрузить драйвер устройства.
										|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
			ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", ПодключенноеУстройство.Наименование);
			ВыходныеПараметры = Новый Массив();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(ОписаниеОшибки);
		Иначе
			Параметры            = ПодключенноеУстройство.Параметры;
			ПараметрыПодключения = ПодключенноеУстройство.ПараметрыПодключения;
			Если ОбработчикДрайвераМодуль = Неопределено Тогда
				ТекстСообщения = НСтр("ru='Не удалось подключить обработчик драйвера.'");
				ВыходныеПараметры = Новый Массив();
				ВыходныеПараметры.Добавить(999);
				ВыходныеПараметры.Добавить(ТекстСообщения);
				ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
			Иначе
				#Если ВебКлиент Тогда
				// Принудительно заменяем асинхронный обработчик на синхронный, так как работам в синхронном режиме.
				Если ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверАсинхронноКлиент Тогда
					ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент;
				КонецЕсли;
				#КонецЕсли
				// Вызов метода выполнения команды у обработчика.
				Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду(Команда, ВходныеПараметры, ВыходныеПараметры, 
																ОбъектДрайвера, Параметры, ПараметрыПодключения); 
			КонецЕсли
		КонецЕсли;
	Иначе
		// Сообщить об ошибке, что устройство не подключено.
		ВыходныеПараметры = Новый Массив();
		ТекстСообщения = НСтр("ru='Устройство не подключено. Перед выполнением операции устройство должно быть подключено.'");
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ТекстСообщения);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти  

#Область ПроцедурыИФункцииДляРаботыСПодключаемымОборудованием

// Выполняет дополнительную команду к драйверу, не требующую предварительного подключения устройства в системе.
//
Функция ВыполнитьДополнительнуюКоманду(Команда, ВходныеПараметры, ВыходныеПараметры, Идентификатор, Параметры) Экспорт
	
	Результат = Ложь;
	
	// Поиск подключенного устройства.
	ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Идентификатор);
	
	Если ПодключенноеУстройство = Неопределено Тогда
		
		ДанныеОборудования = МенеджерОборудованияВызовСервера.ПолучитьДанныеУстройства(Идентификатор);
		
		времПараметрыПодключения = Новый Структура();
		времПараметрыПодключения.Вставить("ТипОборудования", ДанныеОборудования.ТипОборудованияИмя);
		
		ОбъектДрайвера = ПолучитьОбъектДрайвера(ДанныеОборудования);
		Если ОбъектДрайвера = Неопределено Тогда
			// Сообщить об ошибке, что не удалось загрузить драйвер.
			ТекстСообщения = НСтр("ru='Не удалось загрузить драйвер устройства.
										|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
			ВыходныеПараметры = Новый Массив();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(ТекстСообщения);
			ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
			
		Иначе
			ОбработчикДрайвераМодуль = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(ДанныеОборудования.ОбработчикДрайвера, Не ДанныеОборудования.ВСоставеКонфигурации, ДанныеОборудования.ТипОборудованияИмя);
			Если ОбработчикДрайвераМодуль = Неопределено Тогда
				// Сообщить об ошибке, что не удалось загрузить драйвер.
				ТекстСообщения = НСтр("ru='Не удалось подключить обработчик драйвера.'");
				ВыходныеПараметры = Новый Массив();
				ВыходныеПараметры.Добавить(999);
				ВыходныеПараметры.Добавить(ТекстСообщения);
				ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
			Иначе
				#Если ВебКлиент Тогда
				// Принудительно заменяем асинхронный обработчик на синхронный, так как работам в синхронном режиме.
				Если ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверАсинхронноКлиент Тогда
					ОбработчикДрайвераМодуль = ПодключаемоеОборудованиеУниверсальныйДрайверКлиент;
				КонецЕсли;
				#КонецЕсли
				
				Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду(Команда, ВходныеПараметры, ВыходныеПараметры, ОбъектДрайвера, Параметры, времПараметрыПодключения);
				Если Не Результат Тогда
					ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
				КонецЕсли;
			КонецЕсли
		КонецЕсли;
	Иначе
		// Сообщить об ошибке, что устройство подключено.
		ТекстСообщения = НСтр("ru='Устройство подключено. Перед выполнением операции устройство должно быть отключено.'");
		ВыходныеПараметры = Новый Массив();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ТекстСообщения);
		ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Выполняет команду обработчика драйвера, не требующую предварительного подключения устройства в системе.
//
Функция ВыполнитьКомандуОбработчикаДрайвера(Команда, ВходныеПараметры, ВыходныеПараметры, Идентификатор, Параметры, ПоддержкаАсинхронногоРежима) Экспорт
	
	Результат = Ложь;
	
	Если Идентификатор <> Неопределено Тогда
		ДанныеОборудования = МенеджерОборудованияВызовСервера.ПолучитьДанныеУстройства(Идентификатор);
		ОбработчикДрайвераМодуль = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(ДанныеОборудования.ОбработчикДрайвера, Не ДанныеОборудования.ВСоставеКонфигурации, ДанныеОборудования.ТипОборудованияИмя);
		Если ОбработчикДрайвераМодуль <> Неопределено Тогда
			Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду(Команда, ВходныеПараметры, ВыходныеПараметры, Неопределено, Параметры, Неопределено);
			ПоддержкаАсинхронногоРежима = ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима();
		КонецЕсли;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти  

#Область ПроцедурыИФункцииПодключенияОтключенияОборудованияВФорме

// Начать подключение необходимых типов оборудования при открытии формы.
//
// Параметры:
//	Форма - УправляемаяФорма
//	ПоддерживаемыеТипыПодключаемогоОборудования - Строка
//		Содержит перечень типов подключаемого оборудования, разделенных запятыми.
//
Процедура НачатьПодключениеОборудованиеПриОткрытииФормы(ОповещениеПриПодключении, Форма, ПоддерживаемыеТипыПодключаемогоОборудования) Экспорт
	
	Форма.ПоддерживаемыеТипыПодключаемогоОборудования = ПоддерживаемыеТипыПодключаемогоОборудования;
	
	Если Форма.ИспользоватьПодключаемоеОборудование Тогда
			
	Если ОповещениеПриПодключении = Неопределено Тогда
		ОповещениеПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершениеПоУмолчанию", МенеджерОборудованияКлиент);
	КонецЕсли;
		НачатьПодключениеОборудованиеПоТипу(ОповещениеПриПодключении, Форма.УникальныйИдентификатор,
											ПреобразоватьСписокСтрокойВМассив(Форма.ПоддерживаемыеТипыПодключаемогоОборудования));
	КонецЕсли;
	
КонецПроцедуры

// Обработчик оповещения по умолчанию
//
Процедура ПодключитьОборудованиеЗавершениеПоУмолчанию(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru='При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

// Начать отключать оборудование по типу при закрытии формы.
//
Процедура НачатьОтключениеОборудованиеПриЗакрытииФормы(ОповещениеПриОтключении, Форма) Экспорт
	
	Если ОповещениеПриОтключении = Неопределено Тогда
		ОповещениеПриОтключении = Новый ОписаниеОповещения("ОтключитьОборудованиеЗавершениеПоУмолчанию", МенеджерОборудованияКлиент);
	КонецЕсли;
	
	НачатьОтключениеОборудованиеПоТипу(ОповещениеПриОтключении, Форма.УникальныйИдентификатор, ПреобразоватьСписокСтрокойВМассив(Форма.ПоддерживаемыеТипыПодключаемогоОборудования));
	
КонецПроцедуры

Процедура ОтключитьОборудованиеЗавершениеПоУмолчанию(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru='При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииДляРаботыСДрайвером

// Проверяет установлен ли драйвер.
//
Функция ДрайверУстановлен(Идентификатор) Экспорт
	
	ДанныеОборудования = МенеджерОборудованияВызовСервера.ПолучитьДанныеУстройства(Идентификатор);
	ОбъектДрайвера = ПолучитьОбъектДрайвера(ДанныеОборудования);
	
	Возврат ОбъектДрайвера <> Неопределено;
	
КонецФункции

#Если Не ВебКлиент Тогда

// Установить или переустановить драйверы помеченные флагами.
//
Процедура ПереустановитьПомеченныеДрайверы() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	МассивРабочихМест = МенеджерОборудованияКлиентПовтИсп.НайтиРабочиеМестаПоИД(ВРег(СистемнаяИнформация.ИдентификаторКлиента));
	Если МассивРабочихМест.Количество() = 0 Тогда
		РабочееМесто = Неопределено
	Иначе
		РабочееМесто = МассивРабочихМест[0];
	КонецЕсли;
	
	// Переустановить драйверы помеченные флагом для переустановки.
	Если ЗначениеЗаполнено(РабочееМесто) Тогда
		СписокОборудования = МенеджерОборудованияВызовСервера.ДрайвераДляПереустановки(РабочееМесто);
		Для Каждого Оборудование Из СписокОборудования Цикл
			Если Оборудование.ДанныеДрайвера.ВСоставеКонфигурации И НЕ Оборудование.ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
				НачатьУстановкуВнешнейКомпоненты(, "ОбщийМакет." + Оборудование.ДанныеДрайвера.ИмяМакетаДрайвера);
			КонецЕсли;
			МенеджерОборудованияВызовСервера.УстановитьПризнакПереустановкиДрайвера(РабочееМесто, Оборудование.ДрайверОборудования, Ложь); 
		КонецЦикла;
	КонецЕсли;
	
	// Установить драйверы помеченные флагом для установки.
	Если ЗначениеЗаполнено(РабочееМесто) Тогда
		СписокОборудования = МенеджерОборудованияВызовСервера.ДрайвераДляУстановки(РабочееМесто);
		Для Каждого Оборудование Из СписокОборудования Цикл
			Если Оборудование.ДанныеДрайвера.ВСоставеКонфигурации И НЕ Оборудование.ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
				ОбъектДрайвера = ПолучитьОбъектДрайвера(Оборудование.ДанныеДрайвера);
				Если ОбъектДрайвера = Неопределено Тогда
					НачатьУстановкуВнешнейКомпоненты(, "ОбщийМакет." + Оборудование.ДанныеДрайвера.ИмяМакетаДрайвера);
				Иначе
					ОтключитьОбъектДрайвера(Оборудование.ДанныеДрайвера);
				КонецЕсли;
			КонецЕсли;
			МенеджерОборудованияВызовСервера.УстановитьПризнакУстановкиДрайвера(РабочееМесто, Оборудование.ДрайверОборудования, Ложь); 
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьУстановкуДрайвераИзДистрибутиваЗавершение(Результат, Параметры) Экспорт
	
	Если Параметры.Свойство("ВременныйФайл") Тогда
		НачатьУдалениеФайлов(, Параметры.ВременныйФайл);
	КонецЕсли;
	Если Параметры.Свойство("КаталогИнсталляции") Тогда
		НачатьУдалениеФайлов(, Параметры.КаталогИнсталляции);
	КонецЕсли;
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриЗавершении, Результат = 0);
	КонецЕсли;
	
КонецПроцедуры

// Начать установку драйвера из дистрибутива поставщика из макета.
//
Процедура НачатьУстановкуДрайвераИзДистрибутиваВМакете(ОповещениеПриЗавершении, ИмяМакета, ИмяФайла) Экспорт
	
	Результат = Ложь;
	// Получение макета с сервера
	СсылкаНаФайл = МенеджерОборудованияВызовСервера.ПолучитьМакетССервера(ИмяМакета);
	ИмяФайлаВрем = ?(ПустаяСтрока(ИмяФайла), "setup.exe", ИмяФайла);
	
	// НачатьПолучениеКаталогаВременныхФайлов 
	ВременныйКаталог   = КаталогВременныхФайлов();
	ВременныйФайл      = ВременныйКаталог + "Model.zip";
	КаталогИнсталляции = ВременныйКаталог + "Model\";
	
	// Распаковка архива дистрибутива во временный каталог.
	Результат = ПолучитьФайл(СсылкаНаФайл, ВременныйФайл, Ложь);
	
	ФайлАрхива = Новый ЧтениеZipФайла();
	ФайлАрхива.Открыть(ВременныйФайл);
	
	Если ФайлАрхива.Элементы.Найти(ИмяФайлаВрем) <> Неопределено Тогда
		// Распаковка дистрибутива
		ФайлАрхива.ИзвлечьВсе(КаталогИнсталляции);
		ФайлАрхива.Закрыть();
		// Запуск инсталлятора
		Параметры = Новый Структура("КаталогИнсталляции, ВременныйФайл, ОповещениеПриЗавершении", КаталогИнсталляции, ВременныйФайл, ОповещениеПриЗавершении);
		Оповещение = Новый ОписаниеОповещения("НачатьУстановкуДрайвераИзДистрибутиваЗавершение", ЭтотОбъект, Параметры);
		НачатьЗапускПриложения(Оповещение, КаталогИнсталляции + ИмяФайлаВрем, КаталогИнсталляции, Истина);
	Иначе
		ТекстОшибки = НСтр("ru='Ошибка установки драйвера из дистрибутива в макете.
							|Файл ""%Файл%"" в макете не найден.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Файл%", ИмяФайлаВрем);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки); 
		НачатьУдалениеФайлов(, ВременныйФайл);
		Если ОповещениеПриЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьУстановкуДрайвераИзДистрибутиваИзБазыЗавершение(Результат, Параметры) Экспорт
	
	НачатьУдалениеФайлов(, Параметры.ВременныйКаталог + "Model\");
	НачатьУдалениеФайлов(, Параметры.ВременныйКаталог + "Model.zip");
	
	Если Параметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриЗавершении, Результат = 0);
	КонецЕсли;
	
КонецПроцедуры

// Начать установку драйвера из дистрибутива поставщика из базы.
//
Процедура НачатьУстановкуДрайвераИзДистрибутиваИзБазы(ОповещениеПриЗавершении, ДанныеДрайвера) Экспорт
	
	Результат = Ложь;
	
	ВременныйКаталог   = КаталогВременныхФайлов();
	ИмяФайлаВрем       = ВременныйКаталог + ДанныеДрайвера.ИмяФайлаДрайвера;
	КаталогИнсталляции = ВременныйКаталог + "Model\";
	
	ПолучитьФайл(ПолучитьНавигационнуюСсылку(ДанныеДрайвера.ДрайверОборудования, "ЗагруженныйДрайвер"), ИмяФайлаВрем, Ложь);
	ВременныйФайл = Новый Файл(ИмяФайлаВрем);
	
	Если ВРег(ВременныйФайл.Расширение) = ".ZIP" Тогда
		// Распаковка дистрибутива
		ФайлАрхива = Новый ЧтениеZipФайла();
		ФайлАрхива.Открыть(ВременныйФайл.ПолноеИмя);
		
		ИмяФайлаУстанавливаемый = "";
		Если ФайлАрхива.Элементы.Найти(ВременныйФайл.ИмяБезРасширения  + ".EXE") <> Неопределено Тогда
			ИмяФайлаУстанавливаемый = ВременныйФайл.ИмяБезРасширения  + ".EXE";
		ИначеЕсли ФайлАрхива.Элементы.Найти("setup.exe") <> Неопределено Тогда
			ИмяФайлаУстанавливаемый = "setup.exe";
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ИмяФайлаУстанавливаемый) Тогда
			// Распаковка дистрибутива
			ФайлАрхива.ИзвлечьВсе(КаталогИнсталляции);
			ФайлАрхива.Закрыть();
			// Запуск инсталлятора
			Параметры = Новый Структура("КаталогИнсталляции, ВременныйФайл, ОповещениеПриЗавершении", КаталогИнсталляции, ИмяФайлаВрем, ОповещениеПриЗавершении);
			Оповещение = Новый ОписаниеОповещения("НачатьУстановкуДрайвераИзДистрибутиваЗавершение", ЭтотОбъект, Параметры);
			НачатьЗапускПриложения(Оповещение, КаталогИнсталляции + ИмяФайлаУстанавливаемый, КаталогИнсталляции, Истина);
		Иначе
			ФайлАрхива.Закрыть();
			ТекстОшибки = НСтр("ru='Ошибка установки драйвера из дистрибутива в архиве.
						|Необходимый файл в архиве не найден.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки); 
			НачатьУдалениеФайлов(, ИмяФайлаВрем);
		КонецЕсли;
	Иначе
		// Запуск инсталлятора
		Параметры = Новый Структура("ВременныйФайл, ОповещениеПриЗавершении", ИмяФайлаВрем, ОповещениеПриЗавершении);
		Оповещение = Новый ОписаниеОповещения("НачатьУстановкуДрайвераИзДистрибутиваЗавершение", ЭтотОбъект, Параметры);
		НачатьЗапускПриложения(Оповещение, ВременныйКаталог + ИмяФайлаВрем, ВременныйКаталог, Истина);
	КонецЕсли;
	
КонецПроцедуры

#Иначе

Процедура НачатьУстановкуРасширенияРаботыСФайламиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		НачатьУстановкуРасширенияРаботыСФайлами();
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодключениеРасширенияРаботыСФайламиЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено И ДополнительныеПараметры.ПредлагатьУстановку Тогда
		Оповещение = Новый ОписаниеОповещения("НачатьУстановкуРасширенияРаботыСФайламиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ТекстСообщения = НСтр("ru='Для продолжении работы необходимо установить расширение для веб-клиента ""1С:Предприятие"". Установить?'");
		ПоказатьВопрос(Оповещение, ТекстСообщения, РежимДиалогаВопрос.ДаНет); 
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОповещениеПриЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Подключено);
	КонецЕсли
	
КонецПроцедуры

#КонецЕсли

// Проверить доступность расширения работы с Файлами.
// 
Процедура ПроверитьДоступностьРасширенияРаботыСФайлами(ОповещениеПриЗавершении, ПредлагатьУстановку = Истина) Экспорт
	
#Если Не ВебКлиент Тогда
	// В тонком и толстом клиентах расширение подключено всегда.
	ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Истина);
	Возврат;
#Иначе
	ДополнительныеПараметры = Новый Структура("ОповещениеПриЗавершении, ПредлагатьУстановку", ОповещениеПриЗавершении, ПредлагатьУстановку);
	Оповещение = Новый ОписаниеОповещения("НачатьПодключениеРасширенияРаботыСФайламиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	НачатьПодключениеРасширенияРаботыСФайлами(Оповещение);
#КонецЕсли
	
КонецПроцедуры

// Отключение объекта драйвера.
//
Процедура ОтключитьОбъектДрайвера(ДанныеДрайвера) Экспорт

	НомерСтрокиМассива = глПодключаемоеОборудование.ПараметрыПодключенияПО.Найти(ДанныеДрайвера.ДрайверОборудования);
	Если НомерСтрокиМассива <> Неопределено Тогда
		глПодключаемоеОборудование.ПараметрыПодключенияПО.Удалить(НомерСтрокиМассива);
	КонецЕсли;
	
КонецПроцедуры

// Получение объекта драйвера
//
Функция ПолучитьОбъектДрайвера(ДанныеДрайвера, ТекстОшибки = Неопределено)
	
	ОбъектДрайвера = Неопределено;
	
	Для Каждого ДрайверПО Из глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования Цикл
		Если ДрайверПО.Ключ = ДанныеДрайвера.ДрайверОборудования  Тогда
			ОбъектДрайвера = ДрайверПО.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;   
	
	Если ОбъектДрайвера = Неопределено Тогда
		Попытка
			
			ProgID = ДанныеДрайвера.ИдентификаторОбъекта;
			Если ПустаяСтрока(ProgID) Тогда
				ОбъектДрайвера = ""; // Драйвер не требуется
			Иначе
				ProgID1 = ?(Найти(ProgID, "|") > 0, Сред(ProgID, 1, Найти(ProgID, "|")-1), ProgID); 
				ProgID2 = ?(Найти(ProgID, "|") > 0, Сред(ProgID, Найти(ProgID, "|")+1), ProgID); 
				Если ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
					ПодключитьВнешнююКомпоненту(ProgID1);
				Иначе
					ИмяОбъекта = Сред(ProgID1, Найти(ProgID1, ".") + 1); 
					Префикс = Сред(ProgID1, 1, Найти(ProgID1, ".")); 
					ProgID2 = Префикс + СтрЗаменить(ИмяОбъекта, ".", "_") + "." + ИмяОбъекта;
					Если ДанныеДрайвера.ВСоставеКонфигурации Тогда
						Результат = ПодключитьВнешнююКомпоненту("ОбщийМакет." + ДанныеДрайвера.ИмяМакетаДрайвера, СтрЗаменить(ИмяОбъекта, ".", "_"));
					Иначе
						СсылкаНаДрайвер = ПолучитьНавигационнуюСсылку(ДанныеДрайвера.ДрайверОборудования, "ЗагруженныйДрайвер");
						Результат = ПодключитьВнешнююКомпоненту(СсылкаНаДрайвер, СтрЗаменить(ИмяОбъекта, ".", "_"));
					КонецЕсли;
				КонецЕсли;
				ОбъектДрайвера = Новый (ProgID2);
			КонецЕсли;
				
		Исключение
			Инфо = ИнформацияОбОшибке();
			ТекстОшибки = Инфо.Описание;
		КонецПопытки;
		
		Если ОбъектДрайвера <> Неопределено Тогда
			глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования.Вставить(ДанныеДрайвера.ДрайверОборудования, ОбъектДрайвера);
			ОбъектДрайвера = глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования[ДанныеДрайвера.ДрайверОборудования];
		КонецЕсли;
		
	КонецЕсли;   
		
	Возврат ОбъектДрайвера;
	
КонецФункции

Процедура НачатьПолучениеОбъектаДрайвераЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	ОбъектДрайвера = Неопределено;
	
	Если Подключено Тогда 
		Попытка
			ОбъектДрайвера = Новый (ДополнительныеПараметры.ProgID);
			Если ОбъектДрайвера <> Неопределено Тогда
				глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования.Вставить(ДополнительныеПараметры.ДрайверОборудования, ОбъектДрайвера);
				ОбъектДрайвера = глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования[ДополнительныеПараметры.ДрайверОборудования];
			КонецЕсли;
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, ОбъектДрайвера);
			Возврат;
		Исключение
			ОбъектДрайвера = Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПриЗавершении, Неопределено);
	
КонецПроцедуры

// Начать получение объекта драйвера.
//
Процедура НачатьПолучениеОбъектаДрайвера(ОповещениеПриЗавершении, ДанныеДрайвера) Экспорт
	
	ОбъектДрайвера = Неопределено;
	
	Для Каждого ДрайверПО Из глПодключаемоеОборудование.ДрайвераПодключаемогоОборудования Цикл
		Если ДрайверПО.Ключ = ДанныеДрайвера.ДрайверОборудования  Тогда
			ОбъектДрайвера = ДрайверПО.Значение;
			ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, ОбъектДрайвера);
			Возврат;
		КонецЕсли;
	КонецЦикла;   
	
	Если ОбъектДрайвера = Неопределено Тогда
			ProgID = ДанныеДрайвера.ИдентификаторОбъекта;
			Если ПустаяСтрока(ProgID) Тогда
				ОбъектДрайвера = ""; // Драйвер не требуется
				ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, ОбъектДрайвера);
			Иначе
				ProgID1 = ?(Найти(ProgID, "|") > 0, Сред(ProgID, 1, Найти(ProgID, "|")-1), ProgID); 
				ProgID2 = ?(Найти(ProgID, "|") > 0, Сред(ProgID, Найти(ProgID, "|")+1), ProgID); 
				
				Если ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
					Параметры = Новый Структура("ProgID, ОповещениеПриЗавершении, ДрайверОборудования", ProgID2, ОповещениеПриЗавершении, ДанныеДрайвера.ДрайверОборудования);
					Оповещение = Новый ОписаниеОповещения("НачатьПолучениеОбъектаДрайвераЗавершение", ЭтотОбъект, Параметры);
					НачатьПодключениеВнешнейКомпоненты(Оповещение, ProgID1);
				Иначе
					ИмяОбъекта = Сред(ProgID1, Найти(ProgID1, ".") + 1); 
					Префикс = Сред(ProgID1, 1, Найти(ProgID1, ".")); 
					ProgID2 = Префикс + СтрЗаменить(ИмяОбъекта, ".", "_") + "." + ИмяОбъекта;
					
					Параметры = Новый Структура("ProgID, ОповещениеПриЗавершении, ДрайверОборудования", ProgID2, ОповещениеПриЗавершении, ДанныеДрайвера.ДрайверОборудования);
					Оповещение = Новый ОписаниеОповещения("НачатьПолучениеОбъектаДрайвераЗавершение", ЭтотОбъект, Параметры);
					Если ДанныеДрайвера.ВСоставеКонфигурации Тогда
						НачатьПодключениеВнешнейКомпоненты(Оповещение, "ОбщийМакет." + ДанныеДрайвера.ИмяМакетаДрайвера, СтрЗаменить(ИмяОбъекта, ".", "_"));
					Иначе
						СсылкаНаДрайвер = ПолучитьНавигационнуюСсылку(ДанныеДрайвера.ДрайверОборудования, "ЗагруженныйДрайвер");
						НачатьПодключениеВнешнейКомпоненты(Оповещение, СсылкаНаДрайвер, СтрЗаменить(ИмяОбъекта, ".", "_"));
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
	КонецЕсли;   
	
КонецПроцедуры

// Установить драйвер оборудования.
//
Процедура УстановитьДрайвер(Идентификатор, ОповещениеИзДистрибутиваПриЗавершении = Неопределено, ОповещениеИзАрхиваПриЗавершении = Неопределено) Экспорт
	
	ДанныеДрайвера = МенеджерОборудованияВызовСервера.ПолучитьДанныеДрайвера(Идентификатор);
	
	Попытка  
		Если ДанныеДрайвера.ВСоставеКонфигурации Тогда
			
			Если ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
			#Если ВебКлиент Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Данный драйвер не поддерживает работу в веб-клиенте.'")); 
			#Иначе
				Если МенеджерОборудованияКлиентПовтИсп.ЭтоLinuxКлиент() Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Данный драйвер не может быть установлен и использован в среде Linux.'")); 
					Возврат;
				КонецЕсли;
				НачатьУстановкуДрайвераИзДистрибутиваВМакете(ОповещениеИзДистрибутиваПриЗавершении, ДанныеДрайвера.ИмяМакетаДрайвера, ДанныеДрайвера.ИмяФайлаДрайвера);
			#КонецЕсли
			Иначе
				НачатьУстановкуВнешнейКомпоненты(ОповещениеИзАрхиваПриЗавершении, "ОбщийМакет." + ДанныеДрайвера.ИмяМакетаДрайвера);
			КонецЕсли;
			
		Иначе
			
			Если ДанныеДрайвера.ПоставляетсяДистрибутивом Тогда
			#Если ВебКлиент Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Данный драйвер не поддерживает работу в веб-клиенте.'")); 
			#Иначе
				Если МенеджерОборудованияКлиентПовтИсп.ЭтоLinuxКлиент() Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Данный драйвер не может быть установлен и использован в среде Linux.'")); 
					Возврат;
				КонецЕсли;
				НачатьУстановкуДрайвераИзДистрибутиваИзБазы(ОповещениеИзДистрибутиваПриЗавершении, ДанныеДрайвера);
			#КонецЕсли
			Иначе
				СсылкаНаДрайвер = ПолучитьНавигационнуюСсылку(ДанныеДрайвера.ДрайверОборудования, "ЗагруженныйДрайвер");
				НачатьУстановкуВнешнейКомпоненты(ОповещениеИзАрхиваПриЗавершении, СсылкаНаДрайвер);
			КонецЕсли;
			
		КонецЕсли;
		
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Произошла ошибка при установке драйвера.'")); 
	КонецПопытки;  
		
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииДляРаботыСПодключаемымОборудованием

// Начать выполнение команды ответственному обработчику драйвера
//
Процедура НачатьВыполнениеКоманды(ОповещениеПриЗавершении, Идентификатор, Команда, ВходныеПараметры) Экспорт
	
	// Поиск подключенного устройства.
	ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Идентификатор);
	
	Если ПодключенноеУстройство <> Неопределено Тогда
		
		ОбработчикДрайвераМодуль = ПодключенноеУстройство.ОбработчикДрайвераМодуль;
		ОбъектДрайвера = ПодключенноеУстройство.ОбъектДрайвера;
		
		Если ОбработчикДрайвераМодуль = Неопределено Или ОбъектДрайвера = Неопределено Тогда
			// Сообщить об ошибке, что не удалось загрузить драйвер.
			ТекстОшибки = НСтр("ru='Не удалось подключить обработчик драйвера или загрузить драйвер.'");
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании();
			РезультатВыполнения.Результат = Ложь;
			РезультатВыполнения.ОписаниеОшибки = ТекстОшибки; 
			ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, РезультатВыполнения);
		Иначе
			Если ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима() Тогда
				ОбработчикДрайвераМодуль.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, Команда, ВходныеПараметры,
					 ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
			Иначе
				ТекстОшибки = "";
				ВыходныеПараметры = Неопределено;
				Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду(Команда, ВходныеПараметры,
					ВыходныеПараметры,  ОбъектДрайвера, ПодключенноеУстройство.Параметры, ПодключенноеУстройство.ПараметрыПодключения);
				Если Не Результат Тогда
					Если ВыходныеПараметры.Количество() >= 2 Тогда
						ТекстОшибки = ВыходныеПараметры[1];
					КонецЕсли;
					ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
				КонецЕсли;
				РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Результат, ТекстОшибки, Идентификатор);
				РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
				РезультатВыполнения.Вставить("ОбработчикДрайвераМодуль", ОбработчикДрайвераМодуль);
				РезультатВыполнения.Вставить("ОбъектДрайвера"          , ОбъектДрайвера);
				РезультатВыполнения.Вставить("ПодключенноеУстройство"  , ПодключенноеУстройство);
				ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, РезультатВыполнения);
			КонецЕсли;
		КонецЕсли;
	Иначе
		// Сообщить об ошибке, что устройство не подключено.
		ТекстОшибки = НСтр("ru='Устройство не подключено. Перед выполнением операции устройство должно быть подключено.'");
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании();
		РезультатВыполнения.Результат = Ложь;
		РезультатВыполнения.ОписаниеОшибки = ТекстОшибки; 
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Начать выполнение дополнительной команды к драйверу, не требующую предварительного подключения устройства в системе.
//
Процедура НачатьВыполнениеДополнительнойКоманды(ОповещениеПриЗавершении, Команда, ВходныеПараметры, Идентификатор, Параметры) Экспорт
	
	// Поиск подключенного устройства.
	ПодключенноеУстройство = ПолучитьПодключенноеУстройство(глПодключаемоеОборудование.ПараметрыПодключенияПО, Идентификатор);
	                                                       
	Если ПодключенноеУстройство = Неопределено Тогда
		ДанныеОборудования = МенеджерОборудованияВызовСервера.ПолучитьДанныеУстройства(Идентификатор);
		ПараметрыКоманды = Новый Структура();
		ПараметрыКоманды.Вставить("Команда"           , Команда);
		ПараметрыКоманды.Вставить("ВходныеПараметры"  , ВходныеПараметры);
		ПараметрыКоманды.Вставить("Параметры"         , Параметры);
		ПараметрыКоманды.Вставить("ДанныеОборудования", ДанныеОборудования);
		ПараметрыКоманды.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
		Оповещение = Новый ОписаниеОповещения("НачатьВыполнениеДополнительнойКоманды_Завершение", ЭтотОбъект, ПараметрыКоманды);
		НачатьПолучениеОбъектаДрайвера(Оповещение, ДанныеОборудования);
	Иначе
		// Сообщить об ошибке, что устройство подключено.
		ТекстОшибки = НСтр("ru='Устройство подключено. Перед выполнением операции устройство должно быть отключено.'");
		ВыходныеПараметры = Новый Массив();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ТекстОшибки);
		ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстОшибки, Идентификатор); 
		РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Завершение выполнение дополнительной команды к драйверу.
// Не требует предварительного подключения устройства в системе.
//
Процедура НачатьВыполнениеДополнительнойКоманды_Завершение(ОбъектДрайвера, ПараметрыКоманды) Экспорт
	
	Если ОбъектДрайвера = Неопределено Тогда
		// Сообщить об ошибке, что не удалось загрузить драйвер.
		ТекстОшибки = НСтр("ru='Не удалось загрузить драйвер устройства.
								|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
		ВыходныеПараметры = Новый Массив();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(ТекстОшибки);
		ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
		РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстОшибки); 
		РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
		ВыполнитьОбработкуОповещения(ПараметрыКоманды.ОповещениеПриЗавершении, РезультатВыполнения);
	Иначе
		
		ДанныеОборудования = ПараметрыКоманды.ДанныеОборудования; 
		ОбработчикДрайвераМодуль = МенеджерОборудованияКлиентПовтИсп.ПолучитьОбработчикДрайвера(ДанныеОборудования.ОбработчикДрайвера, Не ДанныеОборудования.ВСоставеКонфигурации, ДанныеОборудования.ТипОборудованияИмя);
		
		Если ОбработчикДрайвераМодуль = Неопределено Тогда
			// Сообщить об ошибке, что не удалось подключить обработчик драйвера.
			ТекстОшибки = НСтр("ru='Не удалось подключить обработчик драйвера.'");
			ВыходныеПараметры = Новый Массив();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(ТекстОшибки);
			ВыходныеПараметры.Добавить(НСтр("ru='Не установлен'"));
			РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстОшибки); 
			РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
			ВыполнитьОбработкуОповещения(ПараметрыКоманды.ОповещениеПриЗавершении, РезультатВыполнения);
		Иначе
			времПараметрыПодключения = Новый Структура();
			времПараметрыПодключения.Вставить("ТипОборудования", ПараметрыКоманды.ДанныеОборудования.ТипОборудованияИмя);
			
			Если ОбработчикДрайвераМодуль.ПоддержкаАсинхронногоРежима() Тогда
				ОбработчикДрайвераМодуль.НачатьВыполнениеКоманды(ПараметрыКоманды.ОповещениеПриЗавершении, ПараметрыКоманды.Команда, ПараметрыКоманды.ВходныеПараметры,
					ОбъектДрайвера, ПараметрыКоманды.Параметры, времПараметрыПодключения);
			Иначе
				ТекстОшибки = "";
				ВыходныеПараметры = Неопределено;
				Результат = ОбработчикДрайвераМодуль.ВыполнитьКоманду(ПараметрыКоманды.Команда, ПараметрыКоманды.ВходныеПараметры,
					ВыходныеПараметры, ОбъектДрайвера, ПараметрыКоманды.Параметры, времПараметрыПодключения);
				Если Не Результат Тогда
					Если ВыходныеПараметры.Количество() >= 2 Тогда
						ТекстОшибки = ВыходныеПараметры[1];
					КонецЕсли;
					ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
				КонецЕсли;
				РезультатВыполнения = ПараметрыВыполненияОперацииНаОборудовании(Результат); 
				РезультатВыполнения.ВыходныеПараметры = ВыходныеПараметры;
				ВыполнитьОбработкуОповещения(ПараметрыКоманды.ОповещениеПриЗавершении, РезультатВыполнения);
			КонецЕсли;
		КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСУстройствамиВвода

// Производит получение события от устройства.
//
Функция ПолучитьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки = "") Экспорт
	
	Результат = Неопределено;
	
	// Поиск обработчика события
	Для Каждого Подключение Из глПодключаемоеОборудование.ПараметрыПодключенияПО Цикл
						  
		Если Подключение.ИсточникСобытия = ОписаниеСобытия.Источник
		 Или (ПустаяСтрока(Подключение.ИсточникСобытия) И Подключение.ИменаСобытий <> Неопределено) Тогда
		   
		   // Ищем среди подключенного оборудования устройство с полученным событием.
		    Событие = Подключение.ИменаСобытий.Найти(ОписаниеСобытия.Событие);
		    Если Событие <> Неопределено Тогда
		    	
		    	Если Подключение.ОбъектДрайвера = Неопределено Тогда
		    		// Сообщить об ошибке, что не удалось загрузить драйвер.
		    		ОписаниеОшибки = НСтр("ru='""%Наименование%"": Не удалось загрузить драйвер устройства.
		    									|Проверьте, что драйвер корректно установлен и зарегистрирован в системе.'");
		    		ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%Наименование%", Подключение.Наименование);
		    		Продолжить;
		    	КонецЕсли;
		    	
		    	ВходныеПараметры  = Новый Массив();
		    	ВходныеПараметры.Добавить(ОписаниеСобытия.Событие);
		    	ВходныеПараметры.Добавить(ОписаниеСобытия.Данные);
		    	ВыходныеПараметры = Неопределено;
		    	
		    	// Обрабатываем сообщение
		    	РезультатОбработки = ВыполнитьКоманду(Подключение.Ссылка, "ОбработатьСобытие", ВходныеПараметры, ВыходныеПараметры);
		    	Если РезультатОбработки Тогда
		    		// Оповещаем 
		    		Результат = Новый Структура();
		    		Результат.Вставить("ИмяСобытия", ВыходныеПараметры[0]);
		    		Результат.Вставить("Параметр",   ВыходныеПараметры[1]);
		    		Результат.Вставить("Источник",   "ПодключаемоеОборудование");
		    	КонецЕсли;
		    	
		    	// Оповещаем драйвер о завершении обработки события.
		    	ВходныеПараметры.Очистить();
		    	ВходныеПараметры.Добавить(РезультатОбработки);
		    	ВыполнитьКоманду(Подключение.Ссылка, "ЗавершитьОбработкуСобытия", ВходныеПараметры, ВыходныеПараметры);
		    КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Производит обработку данных события, полученных от клиента.
//
Функция ОбработатьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки = "") Экспорт

	Результат = Истина;
	
	// Поиск обработчика события
	Для Каждого Подключение Из глПодключаемоеОборудование.ПараметрыПодключенияПО Цикл
						  
		Если Подключение.ИсточникСобытия = ОписаниеСобытия.Источник
		 Или (ПустаяСтрока(Подключение.ИсточникСобытия) И Подключение.ИменаСобытий <> Неопределено) Тогда
		   
		   // Ищем среди подключенного оборудования устройство с полученным событием.
		    Событие = Подключение.ИменаСобытий.Найти(ОписаниеСобытия.Событие);
		    Если Событие <> Неопределено Тогда
				
				ОбработчикДрайвераМодуль = Подключение.ОбработчикДрайвераМодуль;
				ОбъектДрайвера = Подключение.ОбъектДрайвера;
				
				ВыходныеПараметры = Новый Массив();
				
				// Обрабатываем сообщение
				Результат = ОбработчикДрайвераМодуль.ОбработатьСобытие(ОбъектДрайвера, Подключение.Параметры, Подключение.ПараметрыПодключения, ОписаниеСобытия.Событие, ОписаниеСобытия.Данные, ВыходныеПараметры);
				// Обрабатываем сообщение
				Если Результат Тогда
					// Оповещаем 
					Оповестить(ВыходныеПараметры[0], ВыходныеПараметры[1], "ПодключаемоеОборудование");
				КонецЕсли;
				ОбработчикДрайвераМодуль.ЗавершитьОбработкуСобытия(ОбъектДрайвера, Подключение.Параметры, Подключение.ПараметрыПодключения, ВыходныеПараметры);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти