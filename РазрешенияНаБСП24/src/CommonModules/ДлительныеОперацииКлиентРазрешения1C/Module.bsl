
#Область ПрограммныйИнтерфейс

Процедура ВыполнитьВФонеИОжидатьЗавершение(Знач ИмяПроцедуры, Знач ПараметрыПроцедуры, Знач ПараметрыВыполнения, ФормаВладелец, ИмяПроцедурыЗавершение) Экспорт
	
	ДлительнаяОперация 		= ДлительныеОперацииВызовСервераРазрешения1C.ВыполнитьВФоне(ИмяПроцедуры, ПараметрыПроцедуры, ПараметрыВыполнения);
	ДополнительныеПараметры = Новый Структура("ИдентификаторЗадания, ФормаВладелец, ИмяПроцедурыЗавершение", ДлительнаяОперация.ИдентификаторЗадания, ФормаВладелец, ИмяПроцедурыЗавершение);
	ОповещениеОЗавершении 	= Новый ОписаниеОповещения("ВыполнитьВФонеЗавершение", ДлительныеОперацииКлиентРазрешения1C, ДополнительныеПараметры);	
	
	ПараметрыОжидания 		= ДлительныеОперацииКлиент.ПараметрыОжидания(ФормаВладелец);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения 	= Истина;
	ПараметрыОжидания.ВыводитьСообщения 			= Истина;
	ПараметрыОжидания.Интервал 						= 5;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительныеОперацииВызовСервераРазрешения1C.ВывестиСообщенияДлительнойОперации(ДополнительныеПараметры.ИдентификаторЗадания, ДополнительныеПараметры.ФормаВладелец.УникальныйИдентификатор);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(ДополнительныеПараметры.ИмяПроцедурыЗавершение, ДополнительныеПараметры.ФормаВладелец, ДополнительныеПараметры);
	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
	
КонецПроцедуры

#КонецОбласти